/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2021 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.api.naming.parser

import io.sarl.api.naming.name.AgentName
import java.net.URI
import java.util.StringTokenizer
import java.util.UUID

/** 
 * Parser of agent names that is accepting URI-based syntax.
 * 
 * <p>The different types of names are: <ul>
 * <li>{@code "agent:[/]{0-2}agentId[#fragmentName]"}</li>
 * <li>{@code "agent:[/]{0-2}contextId/agentId[#fragmentName]"}</li>
 * <li>{@code "agent:[/]{0-2}contextId/spaceId/agentId[#fragmentName]"}</li>
 * </ul>
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.12
 */
class AgentSchemeNameParser extends AbstractSchemeNameParser<AgentName> {

	/** Constructor.
	 *
	 * @param scheme the name scheme that is supported by this parser. By default it is {@link AgentName::SCHEME}.
	 */
	new (scheme : String = AgentName::SCHEME) {
		super(scheme)
	}

	@Pure
	override refactor(name : URI) : URI {
		return name.refactor(1, 3)
	}

	@Pure
	override decode(name : URI) : AgentName {
		val tokenizer = new StringTokenizer(name.path, "/")
		if (tokenizer.hasMoreTokens) {
			val token0 = tokenizer.nextToken
			if (tokenizer.hasMoreTokens) {
				val token1 = tokenizer.nextToken
				if (tokenizer.hasMoreTokens) {
					val token2 = tokenizer.nextToken
					return new AgentName(name, UUID::fromString(token0), UUID::fromString(token1),
						UUID::fromString(token2))
				}
				return new AgentName(name, UUID::fromString(token0), null, UUID::fromString(token1))
			}
			return new AgentName(name, null, null, UUID::fromString(token0))
		}
		return null
	}

}
