/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2021 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.acl.protocol.propose

import io.sarl.acl.message.AclMessage
import io.sarl.acl.message.Performative
import io.sarl.acl.protocol.ProtocolResult
import io.sarl.acl.protocol.ProtocolState
import java.text.MessageFormat
import java.util.UUID
import java.util.Map

/**
 * Initiator side of the Propose Interaction protocol.
 *
 * <center><img src="./doc-files/sequence.png"></center>
 * 
 * @author $Author: sgalland$
 * @author $Author: ngaud$
 * @version $FullVersion$
 * @mavengroupid $Groupid$
 * @mavenartifactid $ArtifactId$
 * @see <a href="http://www.fipa.org/specs/fipa00036/SC00036H.html">FIPA Propose
 *      Interaction Protocol Specification</a>
 */
behavior InitiatorFipaProposeProtocol extends FipaProposeProtocol {

	var resultData : ProtocolResult

	@SuppressWarnings("potential_field_synchronization_problem")
	protected override onCancelled {
		this.resultData = null
	}
	
	final override isInitiatorSide : boolean {
		true
	}

	final override isParticipantSide : boolean {
		false
	}

	/**
	 * At any point in the IP, the initiator of the IP may cancel the
	 * interaction protocol.
	 * 
	 * <p>The semantics of cancel should roughly be interpreted as meaning that the
	 * initiator is no longer interested in continuing the interaction and that
	 * it should be terminated in a manner acceptable to both the Initiator and
	 * the Participant.
	 * 
	 * @param reason the reason of the cancel.
	 * @param reasonParameters is the map that contains the {@code language} used for expressing the given content;
	 *     the {@code ontology} associated to the given content; and the {@code encoding} of the message content.
	 */
	def cancel(reason : Object = null, reasonParameters : Map<String, String> = null) {
		cancelProtocol(reason, reasonParameters, null)
	}

	/**
	 * Send a proposal to the participant. This method must be called by the
	 * initiator.
	 * This method must be called by the initiator.
	 * 
	 * @param participant the participant to request to.
	 * @param proposeDescription the content of the proposal.
	 * @param descriptionParameters is the map that contains the {@code language} used for expressing the given content;
	 *     the {@code ontology} associated to the given content; and the {@code encoding} of the message content.
	 */
	@SuppressWarnings("potential_field_synchronization_problem")
	def propose(participant : UUID = null, proposeDescription : Object, descriptionParameters : Map<String, String> = null) {
		if (participant !== null) {
			participant.initiateAsInitiator
		}
		val part = this.participant
		if (part === null) {
			reportError(Messages::InitiatorFipaProposeProtocol_0)
		} else {
			val rlock = this.lock.readLock
			rlock.lock
			var st : ProtocolState
			try {
				st = this.state
			} finally {
				rlock.unlock
			}
			if (st == ProposeProtocolState::NOT_STARTED) {
				val wlock = this.lock.writeLock
				wlock.lock
				try {
					st = this.state
					if (st == ProposeProtocolState::NOT_STARTED) {
						this.resultData = null
						startTimeoutNotifier
						ProposeProtocolState::WAITING_ANSWER.change(null)
						sendMessage(proposeDescription, Performative::PROPOSE,
							descriptionParameters?.get('language'),
							descriptionParameters?.get('ontology'),
							descriptionParameters?.get('encoding'),
							part)
					} else {
						reportError(MessageFormat::format(Messages::InitiatorFipaProposeProtocol_0, state.name))
					}
				} finally {
					wlock.unlock
				}
			} else {
				reportError(MessageFormat::format(Messages::InitiatorFipaProposeProtocol_0, state.name))
			}
		}
	}

	/**
	 * Replies the answer of the participant to the request (ACCEPT or REJECT).
	 * 
	 * @return the answer (Performative + Content) or {@code null} if no answer.
	 */
	@SuppressWarnings("potential_field_synchronization_problem")
	def getAnswer : ProtocolResult {
		val rlock = this.lock.readLock
		rlock.lock
		try {
			if (this.state == ProposeProtocolState::ACCEPT_PROPOSE || this.state == ProposeProtocolState::REJECT_PROPOSE) {
				return this.resultData
			}
		} finally {
			rlock.unlock
		}
		return null
	}

	@SuppressWarnings("potential_field_synchronization_problem", "discouraged_occurrence_readonly_use")
	on AclMessage [isStarted && !hasFailed] {
		val st = getState
		if (st == ProposeProtocolState::WAITING_ANSWER) {
			switch (occurrence.performative) {
				case REJECT_PROPOSAL: {
					val wlock = this.lock.writeLock
					wlock.lock
					try {
						this.resultData = new ProtocolResult(occurrence.sender, occurrence.performative, occurrence.content.content)
						finish(ProposeProtocolState::REJECT_PROPOSE, new ProposeRejected(this))
					} finally {
						wlock.unlock
					}
				}
				case ACCEPT_PROPOSAL: {
					val wlock = this.lock.writeLock
					wlock.lock
					try {
						this.resultData = new ProtocolResult(occurrence.sender, occurrence.performative, occurrence.content.content)
						finish(ProposeProtocolState::ACCEPT_PROPOSE, new ProposeAccepted(this))
					} finally {
						wlock.unlock
					}
				}
				default: {
					occurrence.performative.reportUnpexectedPerformativeError
				}
			}
		} else {
			reportError(MessageFormat::format(Messages::InitiatorFipaProposeProtocol_1,
				occurrence.performative.name,
				st.name))
		}
	}

}
