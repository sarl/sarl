/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2021 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.acl.tests.protocol.request

import io.sarl.acl.message.Performative
import io.sarl.acl.protocol.AclProtocolId
import io.sarl.acl.protocol.request.InitiatorFipaRequestProtocol
import io.sarl.acl.protocol.request.RequestProtocolState
import io.sarl.acl.tests.protocol.AbstractAclProtocolTest
import io.sarl.sre.test.framework.^extension.PropertyRestoreExtension
import io.sarl.tests.api.extensions.ContextInitExtension
import io.sarl.tests.api.extensions.JavaVersionCheckExtension
import io.sarl.util.SequenceListenerNotifier
import io.sarl.util.SmartListenerCollection
import java.util.UUID
import org.junit.jupiter.api.BeforeEach
import org.junit.jupiter.api.DisplayName
import org.junit.jupiter.api.Tag
import org.junit.jupiter.api.Test
import org.junit.jupiter.api.^extension.ExtendWith

import static extension io.sarl.tests.api.tools.TestAssertions.*
import static extension org.junit.jupiter.api.Assertions.*
import static extension org.mockito.Mockito.*

@ExtendWith(#[
	typeof(ContextInitExtension),
	typeof(JavaVersionCheckExtension),
	typeof(PropertyRestoreExtension)
])
@Tag("acl")
@Tag("unit")
@DisplayName("InitiatorAclRequestProtocol")
class InitiatorAclRequestProtocolTest extends AbstractAclProtocolTest {

	var participant : UUID

	var protocol : InitiatorFipaRequestProtocol

	@BeforeEach
	override setUp : void {
		super.setUp
		this.participant = UUID::randomUUID
		this.protocol = new InitiatorFipaRequestProtocol(this.^agent)
		this.protocol.concurrentCollectionFactory = [new SmartListenerCollection(new SequenceListenerNotifier)]
	}

	def initiateProtocol(testAfterPropose : boolean = true) : Object {
		this.protocol.init
		val requestData = typeof(Object).mock

		RequestProtocolState::NOT_STARTED.assertSame(this.protocol.state)

		this.protocol.request(this.participant, requestData)

		if (testAfterPropose) {
			RequestProtocolState::WAITING_PARTICIPANT_DECISION.assertSame(this.protocol.state)
			assertReceivedAclMessage(Performative::REQUEST, requestData)
			this.protocol.assertChanged
		}

		return requestData
	}

	@Test
	@DisplayName("participant refuses")
	def participantRefuses : void {
		initiateProtocol
		
		var answerData = typeof(Object).mock
		this.protocol.fireEvent(Performative::REFUSE, this.participant, AclProtocolId::FIPA_REQUEST.code, answerData)

		RequestProtocolState::PARTICIPANT_REFUSED.assertSame(this.protocol.state)
		
		this.protocol.result.assertProtocolResult(Performative::REFUSE, this.participant, answerData)
		this.protocol.assertFinished
	}

	@Test
	@DisplayName("participant fails")
	def participantFails : void {
		initiateProtocol
		
		var answerData0 = typeof(Object).mock
		this.protocol.fireEvent(Performative::AGREE, this.participant, AclProtocolId::FIPA_REQUEST.code, answerData0)

		RequestProtocolState::PARTICIPANT_AGREED.assertSame(this.protocol.state)
		
		var answerData1 = typeof(Object).mock
		this.protocol.fireEvent(Performative::FAILURE, this.participant, AclProtocolId::FIPA_REQUEST.code, answerData1)
		
		RequestProtocolState::PARTICIPANT_FAILURE.assertSame(this.protocol.state)

		this.protocol.result.assertProtocolResult(Performative::FAILURE, this.participant, answerData1)
		this.protocol.assertFinished
	}

	@Test
	@DisplayName("participant succeeds with result")
	def participantSucceedsWithResult : void {
		initiateProtocol
		
		var answerData0 = typeof(Object).mock
		this.protocol.fireEvent(Performative::AGREE, this.participant, AclProtocolId::FIPA_REQUEST.code, answerData0)

		RequestProtocolState::PARTICIPANT_AGREED.assertSame(this.protocol.state)
		
		var answerData1 = typeof(Object).mock
		this.protocol.fireEvent(Performative::INFORM, this.participant, AclProtocolId::FIPA_REQUEST.code, answerData1)
		
		RequestProtocolState::DONE_WITH_RESULT.assertSame(this.protocol.state)

		this.protocol.result.assertProtocolResult(Performative::INFORM, this.participant, answerData1)
		this.protocol.assertFinished
	}

	@Test
	@DisplayName("participant succeeds without result")
	def participantSucceedsWithoutResult : void {
		initiateProtocol
		
		var answerData0 = typeof(Object).mock
		this.protocol.fireEvent(Performative::AGREE, this.participant, AclProtocolId::FIPA_REQUEST.code, answerData0)

		RequestProtocolState::PARTICIPANT_AGREED.assertSame(this.protocol.state)
		
		this.protocol.fireEvent(Performative::INFORM, this.participant, AclProtocolId::FIPA_REQUEST.code, null)
		
		RequestProtocolState::DONE_WITHOUT_RESULT.assertSame(this.protocol.state)

		this.protocol.result.assertProtocolResult(Performative::INFORM, this.participant, null)
		this.protocol.assertFinished
	}

	@Test
	@DisplayName("invalid performative")
	def invalidPerformative : void {
		initiateProtocol
		val ex = typeof(TestProtocolError).assertException [
			this.protocol.fireEvent(Performative::CFP, this.participant, AclProtocolId::FIPA_PROPOSE.code, typeof(Object).mock)
		]
		"Unexpected performative CFP".assertInside(ex.message)
		this.protocol.assertFailed
		this.protocol.assertNotFinished
	}

	@Test
	@DisplayName("timeout")
	def timeout : void {
		this.protocol.assertTimeout [
			initiateProtocol(false)
		]
		this.protocol.assertFailed
	}

}
