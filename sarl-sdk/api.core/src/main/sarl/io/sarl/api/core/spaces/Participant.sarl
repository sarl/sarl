/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2026 SARL.io, the original authors and main authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.api.core.spaces

import io.sarl.lang.core.Address
import io.sarl.lang.core.EventListener
import io.sarl.lang.core.SREutils
import io.sarl.lang.core.annotation.PrivateAPI
import io.sarl.lang.core.InformedEventListener

/** 
 * Description of a participant.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.15
 */
class Participant {
		
	val participantAddress : Address

	val participant : EventListener

	val participantType : String

	/** Create a participant.
	 * 
	 * @param address the address of the participant.
	 * @param eventListener the participant.
	 * @param participantType the type of the participant.
	 * @since 3.1
	 */
	@SuppressWarnings("use_reserved_sarl_annotation")
	@PrivateAPI(isCallerOnly = true)
	static def createAndInit(address : Address, eventListener : EventListener, participantType : String = null) : Participant {
		val type = if (participantType === null) {
			if (eventListener instanceof InformedEventListener) {
				eventListener.ownerInstance?.class?.name
			} else {
				null
			}
		} else {
			participantType
		}
		var participant = new Participant(address, eventListener, type)
		var links = new AddressLazyLinks
		links.participant = participant
		SREutils::setSreSpecificData(address, links)
		return participant
	}

	/** Constructor.
	 *
	 * @param address the address of the participant.
	 * @param eventListener the participant.
	 * @param participantType the type of the participant.
	 * @since 3.1
	 */
	protected new (address : Address, eventListener : EventListener, participantType : String = null) {
		this.participantAddress = address
		this.participant = eventListener
		this.participantType = participantType
	}

	/** Replies participant address.
	 *
	 * @return the address of the participant in the page.
	 */
	def getAddress : Address {
		this.participantAddress
	}

	/** Replies the participant.
	 *
	 * @return the event listener of the participant. 
	 */
	def getParticipant : EventListener {
		this.participant
	}

	/** Replies the participant type.
	 * 
	 * @return the type of the participant.
	 * @since 3.1 
	 */
	def getParticipantType : String {
		this.participantType
	}

	
	override equals(obj : Object) : boolean {
		if (obj instanceof Participant) {
			return obj.address == address
		}
		return false
	}

	override hashCode : int {
		address.hashCode
	}

	override toString : String {
		this.participantAddress?.toString
	}
	
}
