/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2026 SARL.io, the original authors and main authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.api.naming.tests.parser

import io.sarl.api.naming.name.BehaviorName
import io.sarl.api.naming.name.SarlName
import io.sarl.api.naming.parser.BehaviorSchemeNameParser
import io.sarl.api.naming.tests.mocks.BehaviorMock
import io.sarl.lang.tests.api.extensions.JavaVersionCheckExtension
import io.sarl.tests.api.extensions.ContextInitExtension
import java.net.URI
import java.util.UUID
import org.junit.jupiter.api.BeforeEach
import org.junit.jupiter.api.Tag
import org.junit.jupiter.api.^extension.ExtendWith

import static extension io.sarl.tests.api.tools.TestAssertions.assertNullOrEmpty
import static extension org.junit.jupiter.api.Assertions.assertEquals
import static extension org.junit.jupiter.api.Assertions.assertNotNull
import static extension org.junit.jupiter.api.Assertions.assertNull
import io.sarl.api.naming.parser.UriBasedNameParser

/**
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
@ExtendWith(#[
	typeof(ContextInitExtension),
	typeof(JavaVersionCheckExtension)
])
@Tag("unit")
@Tag("api")
@Tag("api-naming")
abstract class AbstractBehaviorSchemeNameParserTest {

	protected static val CONTEXT_ID = "2a2c6de4-8327-431b-a916-97a05caafe14"

	protected static val SPACE_ID = "bb0d92be-1730-4453-a284-bd66d31b853f"

	protected static val AGENT_ID = "a0875b30-cff3-4461-9b96-f501e6c6a788"

	protected static val BEHAVIOR_ID = typeof(BehaviorMock).name

	protected static val BEHAVIOR_IDX = 8

	protected var parser : UriBasedNameParser

	private static def m(index : int, e : String*) : String {
		val b = new StringBuilder
		for (elt : e) {
			if (!elt.isNullOrEmpty) {
				b.append("/").append(elt)
			}
		}
		if (index >= 0) {
			b.append("/").append(index)
		}
		return b.toString
	}

	protected static def assertValid(uri : URI, s : String, fragment : String = null, kindex : int,
		elements : String*) : void {
		uri.assertNotNull
		s.assertEquals(uri.scheme)
		m(kindex, elements).assertEquals(uri.path)
		if (fragment.isNullOrEmpty) {
			uri.fragment.assertNullOrEmpty
		} else {
			fragment.assertEquals(uri.fragment)
		}
		uri.host.assertNullOrEmpty
		uri.port.assertEquals(-1)
		uri.query.assertNullOrEmpty
		uri.userInfo.assertNullOrEmpty
	}

	protected static def assertValid(name : SarlName, s : String, fragment : String = null, kindex : int, elements : String*) : void {
		name.assertNotNull
		name.toURI.assertValid(s, fragment, kindex, elements)
		if (fragment.isNullOrEmpty) {
			name.fragment.assertNullOrEmpty
		} else {
			fragment.assertEquals(name.fragment)
		}
		val n = name as BehaviorName
		if (n.contextId === null) {
			n.contextId.assertNull
			n.spaceId.assertNull
			elements.get(0).toUUID.assertEquals(n.agentId)
			elements.get(1).assertEquals(n.behaviorType.name)
			if (elements.length != 2) {
				3.assertEquals(elements.length)
				elements.get(2).assertEquals(BEHAVIOR_IDX)
			}
		} else if (n.spaceId === null) {
			elements.get(0).toUUID.assertEquals(n.contextId)
			n.spaceId.assertNull
			elements.get(1).toUUID.assertEquals(n.agentId)
			elements.get(2).assertEquals(n.behaviorType.name)
			if (elements.length != 3) {
				4.assertEquals(elements.length)
				elements.get(3).assertEquals(BEHAVIOR_IDX)
			}
		} else {
			elements.get(0).toUUID.assertEquals(n.contextId)
			elements.get(1).toUUID.assertEquals(n.spaceId)
			elements.get(2).toUUID.assertEquals(n.agentId)
			elements.get(3).assertEquals(n.behaviorType.name)
			if (elements.length != 4) {
				5.assertEquals(elements.length)
				elements.get(4).assertEquals(BEHAVIOR_IDX)
			}
		}
	}

	private static def toUUID(s : String) : UUID {
		UUID::fromString(s)
	}

	protected static def createURI(value : String) : URI {
		URI::create(value)
	}

	protected def createNormalizedURI(value : String) : URI {
		this.parser.normalize(value.createURI)
	}

	@BeforeEach
	def setUp : void {
		this.parser = new UriBasedNameParser()
		this.parser.addSchemeNameParser(new BehaviorSchemeNameParser)
	}

}
