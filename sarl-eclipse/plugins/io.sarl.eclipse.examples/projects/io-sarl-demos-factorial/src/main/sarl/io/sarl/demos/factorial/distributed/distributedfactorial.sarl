/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2026 SARL.io, the original authors and main authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.demos.factorial.distributed

import io.sarl.api.core.Behaviors
import io.sarl.api.core.DefaultContextInteractions
import io.sarl.api.core.Initialize
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.Logging
import io.sarl.api.core.Schedules
import java.util.concurrent.atomic.AtomicBoolean
import java.util.concurrent.atomic.AtomicLong

/**
 * @author $Author: ngaud$
 * @author $Author: sgalland$
 */
event Factorial {
	var number : long
	var value : long
}

/**
 * @author $Author: ngaud$
 * @author $Author: sgalland$
 */
event Calculate {
	var number : long
}

/**
 * @author $Author: ngaud$
 * @author $Author: sgalland$
 */
event ComputationDone {
	var result : long
}

/**
 * @author $Author: ngaud$
 */
event Hello

/**
 * @author $Author: ngaud$
 */
event Die

/**
 * @author $Author: ngaud$
 * @author $Author: sgalland$
 */
agent DistributedFactorialBootAgent {
	uses DefaultContextInteractions, Lifecycle

	on Initialize {
		typeof(FactorialAgent).spawn
		typeof(FactorialQueryAgent).spawn(10l)
		killMe
	}
}

/**
 * @author $Author: ngaud$
 */
agent FactorialQueryAgent {
	uses Lifecycle, DefaultContextInteractions, Schedules, Logging

	var factValue = new AtomicLong

	var started = new AtomicBoolean(false)

	@SuppressWarnings("potential_inefficient_value_conversion")
	on Initialize {
		loggingName = "QueryAgent"
		if (occurrence.parameters.length > 0) {
			this.factValue.set(occurrence.parameters.get(0) as long)

			info("Auto-detecting Factorial")
			task("discovery-task").every(1000) [
				emit(new Hello)
			]
		} else {
			info("Bad parameter settings, you must specify an integer value to compute the corresponding factorial")
			emit(new Die)
			killMe
		}

	}

	on Hello [!isFromMe && !this.started.get] {
		started.set(true)
		task("discovery-task").cancel
		info("Found Factorial")
		info("Query sent with number " + this.factValue.get)
		emit(new Calculate( this.factValue.get))
	}

	on ComputationDone {
		info(this.factValue.get + "! = " + occurrence.result)
		emit(new Die)
		info("Killing")
		killMe
	}
}

/**
 * @author $Author: ngaud$
 */
agent FactorialAgent {
	uses Lifecycle, Behaviors, Logging, DefaultContextInteractions

	var upto = new AtomicLong

	on Initialize {
		loggingName = "FactorialAgent"
		info("Factorial initialized")
	}

	on Hello [!isFromMe] {
		emit(new Hello)
	}

	on Factorial [occurrence.number < upto] {
		wake(new Factorial(
			occurrence.number + 1,
			occurrence.value * (occurrence.number + 1)))
	}

	on Factorial [occurrence.number == upto] {
		info(this.upto.get + "! = " + occurrence.value)
		emit(new ComputationDone(occurrence.value))
	}

	on Calculate {
		this.upto.set(occurrence.number)
		info("Query for " + this.upto.get + "!")
		wake(new Factorial(0l, 1l))
	}

	on Die {
		info("Killing")
		killMe
	}

}
