/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2026 SARL.io, the original authors and main authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.demos.factorial.basic

import io.sarl.api.core.Behaviors
import io.sarl.api.core.Initialize
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.Logging
import java.util.concurrent.atomic.AtomicLong

/**
 * @author $Author: ngaud$
 * @author $Author: sgalland$
 */
event Factorial {
	var number : long
	var value : long
} 

/**
 * @author $Author: ngaud$
 * @author $Author: sgalland$
 */
agent FactorialAgent {

	uses Lifecycle, Behaviors, Logging

	var upto = new AtomicLong(5l)
	
	  
	@SuppressWarnings("potential_inefficient_value_conversion")
	on Initialize {
		if (occurrence.parameters.length > 0) { /* check the number of parameter specified in the launch configuration */
			this.upto.set(occurrence.parameters.get(0) as long) 
			info("Received Calculate for " + this.upto)   
			wake(new Factorial => [ it.number = 0l ; it.value = 1l ])
		} else {
			info("Bad parameter settings, you must specify an integer value in the arguments of the launching configuration to compute the corresponding factorial")
			killMe
		}
	} 

	on Factorial [ it.number < this.upto.get ] {
		wake(new Factorial => [ it.number = occurrence.number + 1 ; it.value = occurrence.value * (occurrence.number + 1) ])
	}

	on Factorial [ occurrence.number == this.upto.get ] {  
		info("Factorial of " + this.upto.get + " is " + occurrence.value)
		killMe
	}
}
