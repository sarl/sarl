/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-@RELEASE_YEAR@ the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.examples.pingpongspace

import io.sarl.api.core.Behaviors
import io.sarl.api.core.DefaultContextInteractions
import io.sarl.api.core.ExternalContextAccess
import io.sarl.api.core.Initialize
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.Logging
import io.sarl.api.core.OpenEventSpace
import io.sarl.api.core.OpenEventSpaceSpecification
import io.sarl.api.core.ParticipantJoined
import io.sarl.api.core.Schedules
import java.util.UUID

/** 
 * Agent waiting its buddy PongAgent to send him a ping event, and replies to pong event with a ping event
 * @author $Author: sgalland$
 * @author $Author: ngaud$
 */
agent PingAgent {

	uses DefaultContextInteractions, Schedules, Behaviors, Logging, ExternalContextAccess, Lifecycle

	var comspace : OpenEventSpace

	var MAX_ITERATION : Integer

	on Initialize {
		comspace = defaultContext.getOrCreateSpaceWithSpec(typeof(OpenEventSpaceSpecification),
			occurrence.parameters.get(0) as UUID)

		comspace.registerStrongParticipant(asEventListener())

		info("Waiting for Pong Agent")

		MAX_ITERATION = occurrence.parameters.get(1) as Integer
	}

	on ParticipantJoined [(!isFromMe) && (occurrence.spaceID == comspace.spaceID)] {

		val task = task("waiting_for_partner")
		task.every(1000) [
			if (comspace.numberOfStrongParticipants > 1) {
				info("Pong Agent detected.")
				info("Sending Ping #0")
				var evt = new Ping(0)
				evt.source = comspace.getAddress(getID())

				comspace.emit(evt)
				task.cancel
			}
		]
	}

	on Pong [occurrence.index < MAX_ITERATION] {
		info("Receiving Pong #" + occurrence.index)
		info("Sending Ping #" + (occurrence.index + 1))
		comspace.emit(new Ping(occurrence.index + 1))[it == occurrence.source]
	}

	on Pong [occurrence.index == MAX_ITERATION] {
		info("Receiving last Pong #" + occurrence.index)
		info("PingAgent dying")
		killMe
	}

}
