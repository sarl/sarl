/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2023 SARL.io, the Original Authors and Main Authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.demos.fireworks.agents

import io.sarl.api.core.Behaviors
import io.sarl.api.core.DefaultContextInteractions
import io.sarl.api.core.Initialize
import io.sarl.api.core.InnerContextAccess
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.Logging
import io.sarl.api.core.MemberLeft
import io.sarl.api.javafx.AppExit
import io.sarl.demos.fireworks.environment.Positions
import io.sarl.demos.fireworks.events.Freeze
import io.sarl.demos.fireworks.events.Launch
import io.sarl.demos.fireworks.events.RocketReady

/** A rocket launcher that own a rocket in it's inner context.
 *
 * @author <a href="https://github.com/klmp200">Antoine Bartuccio</a>
 * @author <a href="http://www.ciad-lab.fr/nicolas_gaud">Nicolas Gaud</a>
 */
agent RocketLauncher {

	uses Logging, Behaviors, DefaultContextInteractions, InnerContextAccess, Lifecycle

	var x : double
	var y : double
	var fireQuantity : int
	var gravity : double
	var grid : Positions
	var exited = false

	/** Kill itself after an Exit signal.
	 */
	on AppExit [!hasMemberAgent] {
		emit(new AppExit)
		killMe
	}

	/** Transmit an Exit signal to inner context.
	 */
	on AppExit [hasMemberAgent && !exited] {
		exited = true
		innerContext.defaultSpace.emit(this.ID, new AppExit)
	}

	/** Initialization step invoked with parameters.
	 */
	on Initialize [!occurrence.parameters.empty] {
		loggingName = "RocketLauncher-" + this.ID
		x = occurrence.parameters.get(0) as Double
		y = occurrence.parameters.get(1) as Double
		fireQuantity = occurrence.parameters.get(2) as Integer
		gravity = occurrence.parameters.get(3) as Double
		grid = occurrence.parameters.get(4) as Positions

		info("New rocket launcher created")
	}

	/** Initialization step by default.
	 */
	on Initialize [occurrence.parameters.empty] {
		loggingName = "RocketLauncher-" + this.ID
		x = 0.0
		y = 0.0
		fireQuantity = 30
		gravity = 0.5
		grid = null

		info("New rocket launcher created")
		emit(new RocketReady)
	}

	/** Launch a new rocket after a Launch signal.
	 */
	on Launch {
		var vx = Math::random * 2.0
		var vy = Math::random * 5.5 + 2.0
		spawnInContext(typeof(Rocket), innerContext, x, y, vx, vy, gravity, fireQuantity, grid)
	}

	/** Transmit a Freeze signal.
	 */
	on Freeze [!occurrence.isFromMe] {
		innerContext.defaultSpace.emit(this.ID, occurrence)
	}

	/** Launch a new rocket when the previous is destroyed.
	 */
	on MemberLeft [!occurrence.isFromMe && !exited] {
		wake(new Launch)
	}

}
