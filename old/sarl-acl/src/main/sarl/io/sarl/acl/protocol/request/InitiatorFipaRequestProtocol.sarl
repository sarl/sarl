/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2021 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.acl.protocol.request

import io.sarl.acl.message.AclMessage
import io.sarl.acl.message.Performative
import io.sarl.acl.protocol.ProtocolResult
import io.sarl.acl.protocol.ProtocolState
import java.text.MessageFormat
import java.util.UUID
import java.util.Map

/** 
 * Initiator part of the Request interaction protocol.
 *
 * <center><img src="./doc-files/sequence.png"></center>
 * 
 * @author $Author: sgalland$
 * @author $Author: ngaud$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @see <a href="http://www.fipa.org/specs/fipa00026/SC00026H.html">FIPA Request Interaction Protocol Specification</a>
 */
behavior InitiatorFipaRequestProtocol extends FipaRequestProtocol {

	var resultData : ProtocolResult
	
	@SuppressWarnings("potential_field_synchronization_problem")
	protected override onCancelled {
		this.resultData = null
	}
	
	final override isInitiatorSide : boolean {
		true
	}

	final override isParticipantSide : boolean {
		false
	}

	/**
	 * At any point in the IP, the initiator of the IP may cancel the
	 * interaction protocol.
	 * 
	 * <p>The semantics of cancel should roughly be interpreted as meaning that the
	 * initiator is no longer interested in continuing the interaction and that
	 * it should be terminated in a manner acceptable to both the Initiator and
	 * the Participant.
	 * 
	 * @param reason the reason of the cancel.
	 * @param reasonParameters is the map that contains the {@code language} used for expressing the given content;
	 *     the {@code ontology} associated to the given content; and the {@code encoding} of the message content.
	 */
	def cancel(reason : Object = null, reasonParameters : Map<String, String> = null) {
		cancelProtocol(reason, reasonParameters, null)
	}

	/** 
	 * Send a request to the participant.
	 * This method must be called by the initiator.
	 * 
	 * @param participant the participant to request to.
	 * @param requestDescription the description of the request.
	 * @param descriptionParameters is the map that contains the {@code language} used for expressing the given content;
	 *     the {@code ontology} associated to the given content; and the {@code encoding} of the message content.
	 */
	@SuppressWarnings("potential_field_synchronization_problem")
	def request(participant : UUID = null, requestDescription : Object, descriptionParameters : Map<String, String> = null) {
		if (participant !== null) {
			participant.initiateAsInitiator
		}
		val part = this.participant
		if (part === null) {
			reportError(Messages::InitiatorFipaRequestProtocol_0)
		} else {
			val rlock = this.lock.readLock
			rlock.lock
			var st : ProtocolState
			try {
				st = this.state
			} finally {
				rlock.unlock
			}
			if (st == RequestProtocolState::NOT_STARTED) {
				val wlock = this.lock.writeLock
				wlock.lock
				try {
					st = this.state
					if (st == RequestProtocolState::NOT_STARTED) {
						this.resultData = null
						RequestProtocolState::WAITING_PARTICIPANT_DECISION.change(null)
						startTimeoutNotifier
						sendMessage(requestDescription, Performative::REQUEST,
							descriptionParameters?.get('language'),
							descriptionParameters?.get('ontology'),
							descriptionParameters?.get('encoding'),
							part)
					} else {
						reportError(MessageFormat::format(Messages::InitiatorFipaRequestProtocol_0, state.name))
					}
				} finally {
					wlock.unlock
				}
			} else {
				reportError(MessageFormat::format(Messages::InitiatorFipaRequestProtocol_0, state.name))
			}
		}
	}

	/** Replies the result sent by the participant. */
	@SuppressWarnings("potential_field_synchronization_problem")
	def getResult : ProtocolResult {
		val rlock = this.lock.readLock
		rlock.lock
		try {
			if (this.state == RequestProtocolState::DONE_WITHOUT_RESULT || this.state == RequestProtocolState::DONE_WITH_RESULT
				|| this.state == RequestProtocolState::PARTICIPANT_REFUSED || this.state == RequestProtocolState::PARTICIPANT_FAILURE) {
				return this.resultData
			}
		} finally {
			rlock.unlock
		}
		return null
	}

	@SuppressWarnings("potential_field_synchronization_problem", "discouraged_occurrence_readonly_use")
	on AclMessage [isStarted && !hasFailed] {
		val st = getState
		if (st == RequestProtocolState::WAITING_PARTICIPANT_DECISION) {
			switch (occurrence.performative) {
				case REFUSE: {
					val wlock = this.lock.writeLock
					wlock.lock
					try {
						this.resultData = new ProtocolResult(occurrence.sender, occurrence.performative, occurrence.content.content)
						RequestProtocolState::PARTICIPANT_REFUSED.finish(new RequestRefused(this))
					} finally {
						wlock.unlock
					}
				}
				case AGREE: {
					val wlock = this.lock.writeLock
					wlock.lock
					try {
						this.resultData = new ProtocolResult(occurrence.sender, occurrence.performative, occurrence.content.content)
						RequestProtocolState::PARTICIPANT_AGREED.change(new RequestAgreed(this))
					} finally {
						wlock.unlock
					}
				}
				default: {
					occurrence.performative.reportUnpexectedPerformativeError
				}
			}
		} else if (st == RequestProtocolState::PARTICIPANT_AGREED) {
			switch (occurrence.performative) {
				case FAILURE: {
					val wlock = this.lock.writeLock
					wlock.lock
					try {
						this.resultData = new ProtocolResult(occurrence.sender, occurrence.performative, occurrence.content.content)
						RequestProtocolState::PARTICIPANT_FAILURE.finish(new RequestFailed(this))
					} finally {
						wlock.unlock
					}
				}
				case INFORM: {
					val res = getResult
					if (res !== null) {
						reportError(Messages::InitiatorFipaRequestProtocol_3)
					} else {
						val data = occurrence.content.content
						val wlock = this.lock.writeLock
						wlock.lock
						try {
							this.resultData = new ProtocolResult(occurrence.sender, occurrence.performative, data)
							(if (data === null) RequestProtocolState::DONE_WITHOUT_RESULT else RequestProtocolState::DONE_WITH_RESULT).finish(
								new RequestDone(this))
						} finally {
							wlock.unlock
						}
					}
				}
				default: {
					occurrence.performative.reportUnpexectedPerformativeError
				}
			}
		} else {
			reportError(MessageFormat::format(Messages::InitiatorFipaRequestProtocol_2,
				occurrence.performative.name,
				st.name))
		}
	}

}
