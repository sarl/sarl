/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2025 SARL.io, the Original Authors and Main Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.bspl.api.protocol.impl

import io.sarl.bspl.api.memory.KnowledgeID
import io.sarl.bspl.api.memory.LocalStateManager
import io.sarl.bspl.api.protocol.impl.ProtocolRole
import io.sarl.lang.core.SpaceID
import java.util.Map
import java.util.UUID
import java.util.stream.Stream

/**
 * Abstract implementation of a general protocol skill. It represents the super type for all
 * the skills dedicated to specific BSPL protocols.
 *
 * <p>The protocol skill is unique for a single agent (because of the unicity of skills
 * from the SARL metamodel). Therefore, the protocol skill could manage multiple
 * protocol instances.
 *
 * <p>This skill provides protocol-independent functions to the agents.
 * The protocol-specific functions are provided by the sub-skills.
 *
 * @author $Author: stedeschi$
 * @author $Author: sgalland$
 * @author $Author: mbaldoni$
 * @author $Author: cbaroglio$
 * @author $Author: rmicalizio$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.15
 */
abstract skill ProtocolSkill implements ProtocolCapacity {

	uses LocalStateManager
	
	val boundParams = <KnowledgeID>newTreeSet(null)

	val protocolSpaces = <ProtocolSpace>newArrayList

	/** Constructor with an initial protocol instance.
	 *
	 * @param protocolSpace the initial space that is supporting the protocol instance.
	 */
	new (protocolSpace : ProtocolSpace) {
		this.protocolSpaces.add(protocolSpace)
	}

	final synchronized override addProtocolSpace(protocolSpace : ProtocolSpace) {
		this.protocolSpaces.add(protocolSpace)
	}

	synchronized override getProtocolParticipants() : Map<SpaceID, Map<ProtocolRole, Stream<UUID>>> {
		var res = <SpaceID, Map<ProtocolRole, Stream<UUID>>>newTreeMap(null)
		for (^space : protocolSpaces) {
			val agents = <ProtocolRole,Stream<UUID>>newTreeMap(null)
			for (rolePlayers : ^space.rolePlayers.entrySet) {
				agents.put(rolePlayers.key, rolePlayers.value.stream.map [it.ID])
			}
		}
		return res
	}

	synchronized override getProtocolParticipants(role : ProtocolRole) : Map<SpaceID, Stream<UUID>> {
		val res = <SpaceID, Stream<UUID>>newTreeMap(null)
		for (^space : protocolSpaces) {
			res.put(^space.spaceID, ^space.getRolePlayers(role))
		}
		return res
	}
	
	/** Bind a knowledge according to the BSPL specification.
	 *
	 * @param id the identifier of the knowledge to bind.
	 * @throws ParameterAlreadyBoundException the parameter/knowledge is already bound. It is not allowed in BSPL specification.
	 */
	protected final def bind(id : KnowledgeID) {
		if (!this.boundParams.add(id)) {
			throw new ParameterAlreadyBoundException(id)
		}
	}

	/** Unbind a knowledge according to the BSPL specification.
	 *
	 * @param id the identifier of the knowledge to unbind.
	 */
	protected final def unbind(id : KnowledgeID) {
		this.boundParams.remove(id)
	}
	
	/** Replies if the given knowledge is bound.
	 *
	 * @param id the identifier of the knowledge to search for.
	 * @return {@code true} if the parameter with the given identifier is bound.
	 */
	protected final def isBound(id : KnowledgeID) : boolean {
		id.isDefined && this.boundParams.contains(id)
	}

	/** Replies the protocol instance that corresponds to the scope of the
	 * given knowledge identifier, i.e., the first key of the given identifier.
	 *
	 * @param id the identifier of the knowledge to search for.
	 * @return the protocol instance or {@code null}.
	 */
	protected final def findSpaceMachtingScope(scope : KnowledgeID) : ProtocolSpace {
		if (scope.keys.isEmpty) {
			return null
		}
		val spaceIDStr = scope.keys.get(0)
		if (!spaceIDStr.isNullOrEmpty) {
			val spaceID = UUID::fromString(spaceIDStr);
			for (^space : protocolSpaces) {
				if (^space.spaceID == spaceID) {
					return ^space
				}
			}
		}
		return null
	}

	//TODO: removeProtocolSpace
	
}
