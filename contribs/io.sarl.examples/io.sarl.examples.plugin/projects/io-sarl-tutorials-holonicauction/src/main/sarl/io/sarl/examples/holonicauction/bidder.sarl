/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-@RELEASE_YEAR@ the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.examples.holonicauction

import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.core.Logging
import java.util.Random

/**
 * @author $Author: sgalland$
 */
agent Bidder {
	
	uses DefaultContextInteractions, Lifecycle, Logging

	val random = new Random()
	var maxPrice : float
	var myLastBid : float

	on Initialize {
		info("Bidder created")
		maxPrice = random.nextFloat() * 1000f + 100f
		info(maxPrice)
	}

	on Price {
		if(occurrence.price == myLastBid) {
			info("I do not bet, I am the winner with :" + myLastBid)
		} else {
			if(occurrence.price < maxPrice) {
				info("Receiving the price: " + occurrence.price)
				var priceIncrease = random.nextFloat() * 50f
				if(priceIncrease > 0) {
					var newPrice = occurrence.price + priceIncrease

					info("My new price is: " + newPrice)
					if(newPrice <= maxPrice) {
						info("Send my bid: " + newPrice)
						emit(new Bid(newPrice)) [it.ID == defaultContext.getID]
						myLastBid = newPrice
					} else {
						info(" I give up, this is beyond my resources : " + myLastBid)
					}
				}
			}
			else {
				info("I dropped to " + myLastBid)
			}
		}
	}

	on StopAuction {
		info("Auctioneer wants to kill me")
		killMe
	}

}

