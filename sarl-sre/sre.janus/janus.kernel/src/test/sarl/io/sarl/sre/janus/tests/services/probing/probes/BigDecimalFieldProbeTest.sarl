/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2025 SARL.io, the Original Authors and Main Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.sre.janus.tests.services.probing.probes

import io.sarl.api.naming.name.SarlName
import io.sarl.api.naming.name.SpaceName
import io.sarl.api.naming.namespace.FieldAccess
import io.sarl.api.probing.IProbeListener
import io.sarl.api.probing.IProbeReleaseListener
import io.sarl.api.probing.Probe
import io.sarl.lang.tests.api.extensions.JavaVersionCheckExtension
import io.sarl.sre.janus.services.probing.probes.BigDecimalFieldProbe
import io.sarl.sre.janus.tests.framework.SreTestUtilities
import io.sarl.tests.api.extensions.ContextInitExtension
import io.sarl.tests.api.extensions.PropertyRestoreExtension
import io.sarl.tests.api.Nullable
import java.math.BigDecimal
import java.net.URI
import java.util.UUID
import org.junit.jupiter.api.BeforeEach
import org.junit.jupiter.api.DisplayName
import org.junit.jupiter.api.Nested
import org.junit.jupiter.api.Tag
import org.junit.jupiter.api.Test
import org.junit.jupiter.api.^extension.ExtendWith
import org.mockito.ArgumentCaptor

import static org.mockito.ArgumentMatchers.*
import static org.mockito.Mockito.*

import static extension io.sarl.tests.api.tools.TestMockito.mock
import static extension org.junit.jupiter.api.Assertions.assertEquals
import static extension org.junit.jupiter.api.Assertions.assertFalse
import static extension org.junit.jupiter.api.Assertions.assertNull
import static extension org.junit.jupiter.api.Assertions.assertSame
import static extension org.junit.jupiter.api.Assertions.assertTrue
import static extension org.mockito.Mockito.spy
import static extension org.mockito.Mockito.times
import static extension org.mockito.Mockito.verify

/**
 * @author <a href="http://www.ciad-lab.fr/stephane_galland">St&eacute;phane Galland</a>
 * @version janus.kernel 3.0.15.1 20250911-224826
 * @mavengroupid io.sarl.sre.janus
 * @mavenartifactid janus.kernel
 */
@ExtendWith(#[
	typeof(ContextInitExtension),
	typeof(JavaVersionCheckExtension),
	typeof(PropertyRestoreExtension)
])
@DisplayName("unit: BigDecimalFieldProbe test")
@SuppressWarnings("all")
abstract class BigDecimalFieldProbeTest {

	package val INIT_VALUE = BigDecimal::valueOf(34.5)

	package val N_VALUE = BigDecimal::valueOf(56.7)

	@Nullable
	var field : FieldAccess

	@Nullable
	var uri : URI

	@Nullable
	var name : String

	@Nullable
	var releaseListener : IProbeReleaseListener

	@Nullable
	var probeListener : IProbeListener

	@Nullable
	var probe : BigDecimalFieldProbe

	@Nullable
	var nameObject : SarlName

	@BeforeEach
	def setUp : void {
		this.nameObject = typeof(SarlName).mock
		when(this.nameObject.scheme).thenReturn(SpaceName::SCHEME)
		this.field = createField(this.nameObject)
		this.releaseListener = typeof(IProbeReleaseListener).mock
		this.probeListener = typeof(IProbeListener).mock
		this.uri = URI::create("context://mycontext#myfield")
		this.name = UUID::randomUUID.toString
		this.probe = new BigDecimalFieldProbe(this.releaseListener, this.name, this.uri, this.field, [SreTestUtilities::newFactories]) 
		this.probe.addProbeListener(this.probeListener)
	}

	protected def isInitialized : boolean {
		true
	}

	protected def createField(name : SarlName) : FieldAccess {
		var jfield = this.class.getDeclaredField("field")
		var faccess = new FieldAccess(name, jfield, this, true)
		return faccess.spy
	}

	@Test
	def getValue : void {
		this.probe.invalid.assertFalse

		if (isInitialized) {
			INIT_VALUE.assertEquals(this.probe.value)
		} else {
			this.probe.value.assertNull
		}

		this.probe.invalid.assertFalse

		this.releaseListener.verify(never).onProbeReleasedProbe(any)

		var x = this.field.verify(1.times).get

		this.probeListener.verify(never).probeValueChanged(any, anyBoolean)
	}

	@Test
	def setValue_nosync : void {
		this.probe.value = N_VALUE

		this.releaseListener.verify(never).onProbeReleasedProbe(any)

		this.field.verify(never).set(any)

		this.probeListener.verify(never).probeValueChanged(any, anyBoolean)
	}

	@Test
	def setValue_sync : void {
		this.probe.value = N_VALUE

		this.probe.sync

		this.releaseListener.verify(never).onProbeReleasedProbe(any)

		var capture0 = ArgumentCaptor::forClass(typeof(Object))
		this.field.verify(1.times).set(capture0.capture)
		N_VALUE.assertEquals(capture0.value)

		var capture1 = ArgumentCaptor::forClass(typeof(Probe))
		var capture2 = ArgumentCaptor::forClass(typeof(boolean))
		this.probeListener.verify(1.times).probeValueChanged(capture1.capture, capture2.capture.booleanValue)
		this.probe.assertSame(capture1.value)
		capture2.value.assertTrue
	}
	
	@Test
	def sync : void {
		this.probe.value = N_VALUE

		this.probe.sync

		this.releaseListener.verify(never).onProbeReleasedProbe(any)

		var capture0 = ArgumentCaptor::forClass(typeof(Object))
		this.field.verify(1.times).set(capture0.capture)
		N_VALUE.assertEquals(capture0.value)

		var capture1 = ArgumentCaptor::forClass(typeof(Probe))
		var capture2 = ArgumentCaptor::forClass(typeof(boolean))
		this.probeListener.verify(1.times).probeValueChanged(capture1.capture, capture2.capture.booleanValue)
		this.probe.assertSame(capture1.value)
		capture2.value.assertTrue
	}

	@Test
	def release : void {
		this.probe.release

		var capture0 = ArgumentCaptor::forClass(typeof(Probe))
		this.releaseListener.verify(1.times).onProbeReleasedProbe(capture0.capture)
		this.probe.assertSame(capture0.value)

		this.field.verify(never).set(any)

		this.probeListener.verify(never).probeValueChanged(any, anyBoolean)
	}

	@Test
	def isActive : void {
		this.probe.isActive.assertTrue
	}

	@Test
	def isInvalid : void {
		this.probe.isInvalid.assertFalse
		this.probe.sync
		this.probe.isInvalid.assertFalse
		this.probe.value = N_VALUE
		this.probe.isInvalid.assertTrue
		this.probe.sync
		this.probe.isInvalid.assertFalse
	}

	/**
	 * @author <a href="http://www.ciad-lab.fr/stephane_galland">St&eacute;phane Galland</a>
	 * @version janus.kernel 3.0.15.1 20250911-224826
	 * @mavengroupid io.sarl.sre.janus
	 * @mavenartifactid janus.kernel
	 */
	@DisplayName("unit: BigDecimalFieldProbe test with BigDecimal")
	@Nested
	@Tag("unit")
	@Tag("janus")
	@Tag("sre-unit")
	static class ObjectBigDecimalFieldProbeTest extends BigDecimalFieldProbeTest {

		var field : BigDecimal = new BigDecimal(INIT_VALUE.toPlainString)

	}

	/**
	 * @author <a href="http://www.ciad-lab.fr/stephane_galland">St&eacute;phane Galland</a>
	 * @version janus.kernel 3.0.15.1 20250911-224826
	 * @mavengroupid io.sarl.sre.janus
	 * @mavenartifactid janus.kernel
	 */
	@DisplayName("unit: BigDecimalFieldProbe test with null BigDecimal")
	@Nested
	@Tag("unit")
	@Tag("janus")
	@Tag("sre-unit")
	static class NullBigDecimalFieldProbeTest extends BigDecimalFieldProbeTest {

		var field : BigDecimal

		protected def isInitialized : boolean {
			false
		}

	}

	/**
	 * @author <a href="http://www.ciad-lab.fr/stephane_galland">St&eacute;phane Galland</a>
	 * @version janus.kernel 3.0.15.1 20250911-224826
	 * @mavengroupid io.sarl.sre.janus
	 * @mavenartifactid janus.kernel
	 */
	@DisplayName("unit: BigDecimalFieldProbe test with final BigDecimal")
	@Nested
	@Tag("unit")
	@Tag("janus")
	@Tag("sre-unit")
	static class FinalObjectBigDecimalFieldProbeTest extends BigDecimalFieldProbeTest {

		val field : BigDecimal = new BigDecimal(INIT_VALUE.toPlainString)

	}

}
