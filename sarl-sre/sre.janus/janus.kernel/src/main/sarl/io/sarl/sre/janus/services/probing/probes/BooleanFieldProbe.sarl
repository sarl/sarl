/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2025 SARL.io, the Original Authors and Main Authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.janus.services.probing.probes

import io.sarl.api.naming.namespace.FieldAccess
import io.sarl.api.probing.IProbeReleaseListener
import io.sarl.sre.janus.internal.Factories
import jakarta.inject.Provider
import java.net.URI
import java.util.concurrent.atomic.AtomicBoolean

/** 
 * Probe implementation is linked to a primitive boolean field, or a field that
 * could be assimilated to a boolean primitive. A probe is thread-safe.
 * This probe make a buffer of the values. In order to be updated, the
 * {@link #sync()} function must be invoked. Then, the new value is read
 * from the associated field, and the probe listeners are notified.
 * 
 * @author $Author: sgalland$
 * @version janus.kernel 3.0.15.0 20250909-115749
 * @mavengroupid io.sarl.sre.janus
 * @mavenartifactid janus.kernel
 * @since 0.12
 */
class BooleanFieldProbe extends AbstractFieldProbe<Boolean> {

	/** Constructor.
	 * 
	 * @param manager the creator of this probe, and its manager.
	 * @param name the name of the probe
	 * @param uri the UI of the field.
	 * @param field the accessor to the probed field.
	 * @param factories the provider of factories tool.
	 */
	new (manager : IProbeReleaseListener, name : String, uri : URI, field : FieldAccess, factories : Provider<Factories>) {
		super(manager, name, uri, typeof(Boolean), field, factories)
	}
	
	protected override areEqualValues(newValue : Boolean, oldValue : Boolean) : boolean {
		(newValue === null && oldValue === null)
		|| (newValue !== null && oldValue !== null && newValue.booleanValue === oldValue.booleanValue)
	}

	@SuppressWarnings("io.sarl.lang.validation.IssueCodes.potential_inefficient_value_conversion")
	protected override toT(value : Object, concreteType : Class<?>) : Boolean {
		switch (concreteType) {
			case typeof(boolean),
			case typeof(Boolean): {
				return value as Boolean
			}
			case typeof(AtomicBoolean): {
				val ab = value as AtomicBoolean
				if (ab === null) {
					return null
				}
				return ab.get
			}
			default: {
				return null
			}
		}
	}
	
	@SuppressWarnings("io.sarl.lang.validation.IssueCodes.potential_inefficient_value_conversion")
	protected override prepareFieldWriting(newValue : Boolean, concreteType : Class<?>, currentValue : Object) : Object {
		switch (concreteType) {
			case typeof(boolean): {
				if (newValue === null) {
					return Boolean.FALSE
				}
				return newValue
			}
			case typeof(Boolean): {
				return newValue
			}
			case typeof(AtomicBoolean): {
				val ab = currentValue as AtomicBoolean
				val cv = if (newValue === null) false else newValue.booleanValue
				if (ab === null) {
					return new AtomicBoolean(cv)
				}
				ab.set(cv)
				return ab
			}
			default: {
				return newValue
			}
		}
	}

}
