/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2025 SARL.io, the Original Authors and Main Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.janus.skills.bic

import io.sarl.api.core.DefaultContextInteractions
import io.sarl.lang.core.Address
import io.sarl.lang.core.Agent
import io.sarl.lang.core.AgentContext
import io.sarl.lang.core.Event
import io.sarl.lang.core.EventSpace
import io.sarl.lang.core.Scope
import io.sarl.lang.core.Skill
import io.sarl.lang.core.Space
import io.sarl.lang.core.SpaceID
import io.sarl.sre.janus.internal.ConfigurableAgentInjector
import io.sarl.sre.janus.internal.Factories
import jakarta.inject.Inject
import jakarta.inject.Provider
import java.util.UUID

import static io.sarl.sre.janus.services.lifecycle.AgentLife.*

/** 
 * Skill to access to the default interaction context.
 * 
 * @author <a href="http://www.sebastianrodriguez.com.ar/">Sebastian Rodriguez</a>
 * @author <a href="http://www.ciad-lab.fr/stephane_galland">St&eacute;phane Galland</a>
 * @version janus.kernel 3.0.15.0 20250909-115749
 * @mavengroupid io.sarl.sre.janus
 * @mavenartifactid janus.kernel
 */
skill DefaultContextInteractionsSkill extends Skill implements DefaultContextInteractions {

	val factories : Provider<Factories>

	val agentLevelInjector : Provider<ConfigurableAgentInjector>

	@Inject
	new (factories : Provider<Factories>, agentLevelInjector : Provider<ConfigurableAgentInjector>) {
		this.factories = factories
		this.agentLevelInjector = agentLevelInjector
	}


	def getDefaultContext : AgentContext {
		val life = getAgentLife(owner, this.factories, this.agentLevelInjector)
		assert life !== null
		val reference = life.defaultContext
		if (reference === null) {
			return null
		}
		return reference.context
	}

	def getDefaultSpace : EventSpace {
		val life = getAgentLife(owner, this.factories, this.agentLevelInjector)
		assert life !== null
		val reference = life.defaultContext
		if (reference === null) {
			return null
		}
		return reference.defaultSpace
	}

	def getDefaultAddress : Address {
		val life = getAgentLife(owner, this.factories, this.agentLevelInjector)
		assert life !== null
		val reference = life.defaultContext
		if (reference === null) {
			return null
		}
		return reference.addressInDefaultSpace
	}

	def isDefaultContext(context : AgentContext) : boolean {
		assert context !== null
		val id = context.ID
		assert id !== null
		return id.isDefaultContext
	}

	def isDefaultContext(contextID : UUID) : boolean {
		assert contextID !== null
		val context = defaultContext
		assert context !== null
		return contextID == context.ID
	}

	def isDefaultSpace(^space : Space) : boolean {
		assert ^space !== null
		val id = ^space.spaceID
		assert id !== null
		return id.isDefaultSpace
	}

	def isDefaultSpace(^space : SpaceID) : boolean {
		assert ^space !== null
		val dspace = defaultSpace
		assert dspace !== null
		return ^space == dspace.spaceID
	}

	def isDefaultSpace(^space : UUID) : boolean {
		assert ^space !== null
		val dspace = defaultSpace
		assert dspace !== null
		val spaceid = dspace.spaceID
		assert spaceid !== null
		return ^space == spaceid.ID
	}

	def isInDefaultSpace(^event : Event) : boolean {
		if (^event !== null) {
			val adr = ^event.source
			if (adr !== null) {
				val spaceid = adr.spaceID
				assert spaceid !== null
				return spaceid.isDefaultSpace
			}
		}
		return false
	}

	override emit(^event : Event, scope : Scope<Address>) {
		assert ^event !== null
		val dspace = defaultSpace
		if (dspace !== null) {
			val adr = defaultAddress
			assert adr !== null
			^event.source = adr
			dspace.emit(ID, ^event, scope)
		}
	}

	def getDefaultParentID : UUID {
		return defaultContext.ID
	}

	def getDefaultParentScope : Scope<Address> {
		val parentID = defaultParentID
		return [it.ID == parentID]
	}

	def emitToParent(^event : Event) {
		assert ^event !== null
		val dspace = defaultSpace
		if (dspace !== null) {
			val adr = defaultAddress
			assert adr !== null
			^event.source = adr
			dspace.emit(ID, ^event, getDefaultParentScope) 
		}
	}

	@Deprecated(since = "3.0.10", forRemoval = true)
	def willReceive(receiver : UUID, ^event : Event) {
		^event.emit [it.ID == receiver]
	}
	
	@Deprecated(since = "3.0.10", forRemoval = true)
	def receive(receiver : UUID, ^event : Event) {
		receiver.willReceive(^event)
	}

	@Deprecated(since = "3.0.10", forRemoval = true)
	def spawn(agentType : Class<? extends Agent>, params : Object*) : UUID {
		throw new UnsupportedOperationException("Use Lifecycle capacity")
	}

}
