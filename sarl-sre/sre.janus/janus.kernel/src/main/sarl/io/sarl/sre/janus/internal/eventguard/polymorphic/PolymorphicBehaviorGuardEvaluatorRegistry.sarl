/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2024 SARL.io, the Original Authors and Main Authors
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.sre.janus.internal.eventguard.polymorphic

import io.sarl.lang.core.Event
import io.sarl.lang.core.IBehaviorGuardEvaluatorReceiver
import io.sarl.lang.core.annotation.PerceptGuardEvaluator
import io.sarl.lang.core.annotation.PrivateAPI
import io.sarl.lang.core.util.ConcurrentCollection
import io.sarl.lang.core.util.ConcurrentSet
import io.sarl.sre.janus.internal.Factories
import io.sarl.sre.janus.internal.MutableBoolean
import io.sarl.sre.janus.internal.ObjectComparator
import io.sarl.sre.janus.internal.TypeHierarchyDictionary
import io.sarl.sre.janus.internal.eventguard.AbstractBehaviorGuardEvaluatorRegistry
import io.sarl.sre.janus.internal.eventguard.IBehaviorGuardEvaluator
import java.util.Comparator
import java.util.concurrent.ConcurrentHashMap
import java.util.concurrent.ConcurrentMap
import javax.inject.Provider

import static extension io.sarl.lang.core.SREutils.*

/** 
 * Registry of all {@code IBehaviorGuardEvaluator}.
 * 
 * @author <a href="http://www.ciad-lab.fr/stephane_galland">St&eacute;phane Galland</a>
 * @version janus.kernel 3.0.14.0 20241106-161408
 * @mavengroupid io.sarl.sre.janus
 * @mavenartifactid janus.kernel
 * @since 0.12
 */
@SuppressWarnings("use_reserved_sarl_annotation")
@PrivateAPI(isCallerOnly = true)
class PolymorphicBehaviorGuardEvaluatorRegistry extends AbstractBehaviorGuardEvaluatorRegistry<PolymorphicBehaviorGuardEvaluator> {

	static val CLASS_COMPARATOR : Comparator<Class<? extends Event>> = [a,b|
		if (a == b) {
			return 0
		}
		if (a === null) {
			return -1
		}
		if (b === null) {
			return 1
		}
		return a.name <=> b.name
	]

	/** 
	 * All registered guard evaluator receivers, indexed by event type.
	 */
	val registeredEvaluators : ConcurrentMap<Class<? extends Event>, ConcurrentSet<PolymorphicBehaviorGuardEvaluator>>

	/** 
	 * Instanciates a new registry linked with the {@link PerceptGuardEvaluator} annotation.
	 * 
	 * <p>The registry will use concurrent data structures.
	 */
	new (factories : Provider<Factories>) {
		super(factories)
		this.registeredEvaluators = new ConcurrentHashMap
	}

	/** Replies the internal data structure for registered listeners.
	 * 
	 * @return the internal data structure
	 */
	@Pure
	override getInternalDataStructureForRegisteredListeners : ConcurrentMap<Class<? extends Event>, ConcurrentSet<PolymorphicBehaviorGuardEvaluator>> {
		this.registeredEvaluators
	}
	
	protected override getInternalEvaluatorMapperForRegisteredListeners : (PolymorphicBehaviorGuardEvaluator)=>IBehaviorGuardEvaluator {
		[
			it
		]
	}
	
	protected override getInternalEvaluatorFilterForRegisteredListeners : (PolymorphicBehaviorGuardEvaluator, Event)=>boolean {
		[
			val filter = $0.filter
			return filter === null || filter.apply($1)
		]
	}

	@Pure
	override getBehaviorGuardEvaluatorsForRegisteredListeners(^event : Event) : ConcurrentCollection<? extends IBehaviorGuardEvaluator> {
		assert ^event !== null
		val eventTypes = TypeHierarchyDictionary::getFlattenHierarchy(^event.class)
		val allEvaluators = this.factories.concurrentCollectionFactory.newCollection
		val mapper = getInternalEvaluatorMapperForRegisteredListeners
		val filter = getInternalEvaluatorFilterForRegisteredListeners
		val dt = internalDataStructureForRegisteredListeners
		for (eventType : eventTypes) {
			val eventSubscribers = dt.get(eventType)
			if (eventSubscribers !== null) {
				for (guardedEvaluator : eventSubscribers) {
					if (filter.apply(guardedEvaluator, ^event)) {
						allEvaluators += mapper.apply(guardedEvaluator)
					}
				}
			}
		}
		return allEvaluators
	}

	@Pure
	override getBehaviorGuardEvaluatorsForDirectAccessListener(^event : Event, listener : Object) : ConcurrentCollection<? extends IBehaviorGuardEvaluator> {
		assert ^event !== null
		assert listener !== null
		val allEvaluators = this.factories.concurrentCollectionFactory.newCollection
		if (listener instanceof IBehaviorGuardEvaluatorReceiver) {
			if (listener.doIsSupportedEvent(^event.class)) {
				val eventTypes = newHashSet
				listener.doGetSupportedEvents(eventTypes)
				for (eventType : eventTypes) {
					if (eventType.isInstance(^event)) {
						allEvaluators += new PolymorphicBehaviorGuardEvaluator(listener, eventType, null)
					}
				}
			}
		}
		return allEvaluators
	}

	override register(listener : Object, filter : (Event)=>boolean, callback : (Object)=>void) {
		if (listener instanceof IBehaviorGuardEvaluatorReceiver) {
			val hasCallback = new MutableBoolean(callback !== null)

			val events = newTreeSet(CLASS_COMPARATOR)
			listener.doGetSupportedEvents(events)

			if (!events.isEmpty) {
				val dt = internalDataStructureForRegisteredListeners
				for (eventType : events) {
					val evaluators = dt.computeIfAbsent(eventType) [
						this.factories.concurrentCollectionFactory.<PolymorphicBehaviorGuardEvaluator>newSet(ObjectComparator::SINGLETON)
					]
					var evaluator = new PolymorphicBehaviorGuardEvaluator(listener, eventType, filter)
					val success = evaluators += evaluator
					if (!success) {
						hasCallback.set(false)
					}
				}
			}

			if (hasCallback.get) {
				callback.apply(listener)
			}
		} else {
			throw new IllegalArgumentException
		}
	}

	override unregister(listener : Object, callback : (Object)=>void) {
		assert listener !== null
		assert !(listener instanceof Class), "illegal argument type. Class is not allowed as listener."
		if (listener instanceof IBehaviorGuardEvaluatorReceiver) {
			val events = newTreeSet(CLASS_COMPARATOR)
			listener.doGetSupportedEvents(events)
			val hasCallback = new MutableBoolean(callback !== null)

			if (!events.isEmpty) {
				val dt = internalDataStructureForRegisteredListeners
				for (eventType : events) {
					val evaluators = dt.get(eventType)
					if (evaluators !== null) {
						val success = evaluators.removeIf [
							listener === it.target
						]
						if (success && hasCallback.get) {
							hasCallback.set(false)
							callback.apply(listener)
						}
					}
				}
			}
		} else {
			throw new IllegalArgumentException
		}
	}
	
}
