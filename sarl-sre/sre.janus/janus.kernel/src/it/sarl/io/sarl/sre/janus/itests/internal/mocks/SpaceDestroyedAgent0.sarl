/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2025 SARL.io, the Original Authors and Main Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License")
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.sre.janus.itests.internal.mocks

import io.sarl.api.core.Behaviors
import io.sarl.api.core.DefaultContextInteractions
import io.sarl.api.core.Initialize
import io.sarl.api.core.InnerContextAccess
import io.sarl.api.core.Lifecycle
import io.sarl.api.core.SpaceDestroyed
import io.sarl.api.core.spaces.OpenEventSpace
import io.sarl.sre.test.framework.events.Bye
import io.sarl.sre.test.framework.events.ByeBye
import io.sarl.sre.test.framework.skills.TestingCapacity
import io.sarl.sre.test.framework.skills.TestingSkill

/**
 * @author <a href="http://www.ciad-lab.fr/stephane_galland">St&eacute;phane Galland</a>
 * @version janus.kernel 3.0.15.0 20250909-115749
 * @mavengroupid io.sarl.sre.janus
 * @mavenartifactid janus.kernel
 */
@SuppressWarnings("potential_field_synchronization_problem", "discouraged_occurrence_readonly_use")
agent SpaceDestroyedAgent0 {

	uses DefaultContextInteractions, InnerContextAccess, Lifecycle, Behaviors

	uses TestingCapacity

	var number : int

	var ^space : OpenEventSpace

	@SuppressWarnings("io.sarl.lang.validation.IssueCodes.potential_inefficient_value_conversion")
	on Initialize {
		setSkill(new TestingSkill(occurrence))
		this.number = occurrence.parameters.get(1) as Integer

		this.^space = null
		if (occurrence.parameters.size > 2) {
			this.^space = occurrence.parameters.get(2) as OpenEventSpace
			if (this.^space !== null) {
				this.^space.registerStrongParticipant(asEventListener)
			}
		}
	}

	on SpaceDestroyed {
		var msg : String
		if (occurrence.inInnerDefaultSpace) {
			msg = "inner"
		} else if (occurrence.inDefaultSpace) {
			msg = "default"
		} else {
			msg = "other"
		}
		addResult("AG" + this.number + ": space destroyed " + occurrence.source.ID + " " + occurrence.spaceID.ID + " in " + msg + " space")
		if (^space !== null) {
			killMe
		}
	}

	on Bye {
		if (this.^space !== null) {
			this.^space.unregister(asEventListener)
		}
	}

	on ByeBye {
		if (this.^space === null) {
			killMe
		}
	}

}
