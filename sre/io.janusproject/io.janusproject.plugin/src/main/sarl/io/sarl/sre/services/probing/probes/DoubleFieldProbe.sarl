/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2021 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.services.probing.probes

import com.google.common.util.concurrent.AtomicDouble
import io.sarl.api.naming.namespace.FieldAccess
import io.sarl.api.probing.IProbeReleaseListener
import io.sarl.sre.internal.Factories
import java.net.URI
import javax.inject.Provider

/** 
 * Probe implementation is linked to a primitive double field, or a field that
 * could be assimilated to a double primitive. A probe is thread-safe.
 * This probe make a buffer of the values. In order to be updated, the
 * {@link #sync()} function must be invoked. Then, the new value is read
 * from the associated field, and the probe listeners are notified.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.12
 */
class DoubleFieldProbe extends AbstractFieldProbe<Double> {

	/** Constructor.
	 * 
	 * @param manager the creator of this probe, and its manager.
	 * @param name the name of the probe
	 * @param uri the UI of the field.
	 * @param field the accessor to the probed field.
	 * @param factories the provider of factories tool.
	 */
	new (manager : IProbeReleaseListener, name : String, uri : URI, field : FieldAccess, factories : Provider<Factories>) {
		super(manager, name, uri, typeof(Double), field, factories)
	}
	
	protected override areEqualValues(newValue : Double, oldValue : Double) : boolean {
		(newValue === null && oldValue === null)
		|| (newValue !== null && oldValue !== null && newValue.doubleValue === oldValue.doubleValue)
	}

	protected override toT(value : Object, concreteType : Class<?>) : Double {
		switch (concreteType) {
			case typeof(double),
			case typeof(Double): {
				return value as Double
			}
			case typeof(AtomicDouble): {
				val ab = value as AtomicDouble
				if (ab === null) {
					return null
				}
				return ab.get
			}
			default: {
				return null
			}
		}
	}
	
	protected override prepareFieldWriting(newValue : Double, concreteType : Class<?>, currentValue : Object) : Object {
		switch (concreteType) {
			case typeof(double): {
				if (newValue === null) {
					return Double::valueOf(0.0)
				}
				return newValue
			}
			case typeof(Double): {
				return newValue
			}
			case typeof(AtomicDouble): {
				val ab = currentValue as AtomicDouble
				val cv = if (newValue === null) 0.0 else newValue.doubleValue
				if (ab === null) {
					return new AtomicDouble(cv)
				}
				ab.set(cv)
				return ab
			}
			default: {
				return newValue
			}
		}
	}

}
