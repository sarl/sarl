/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2021 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.skills.bic

import io.sarl.core.InnerContextAccess
import io.sarl.lang.core.AgentContext
import io.sarl.lang.core.Event
import io.sarl.lang.core.EventSpace
import io.sarl.lang.core.Skill
import io.sarl.lang.core.Space
import io.sarl.lang.core.SpaceID
import io.sarl.lang.util.ConcurrentSet
import io.sarl.sre.capacities.InternalEventBusCapacity
import io.sarl.sre.internal.ConfigurableAgentInjector
import io.sarl.sre.internal.Factories
import io.sarl.sre.services.context.ContextService
import io.sarl.sre.services.lifecycle.AgentLife
import java.util.UUID
import javax.inject.Inject
import javax.inject.Provider

import static extension io.sarl.sre.services.lifecycle.AgentLife.*

/** 
 * SRE implementation of SARL's {@link InnerContextAccess} built-in capacity.
 * 
 * <p>The inner context instance is stored into the {@link AgentLife}.
 * 
 * @author $Author: srodriguez$
 * @author $Author: ngaud$
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
@SuppressWarnings("potential_field_synchronization_problem")
skill InnerContextAccessSkill extends Skill implements InnerContextAccess {

	uses InternalEventBusCapacity

	val contextService : ContextService

	val factories : Provider<Factories>

	val agentLevelInjector : Provider<ConfigurableAgentInjector>

	/** Constructor.
	 */
	@Inject
	new (service : ContextService, factories : Provider<Factories>, agentTraitInitializer : Provider<ConfigurableAgentInjector>) {
		super()
		this.contextService = service
		this.factories = factories
		this.agentLevelInjector = agentTraitInitializer
	}

	/** Destroy the inner context instance.
	 * 
	 * <p>This function is thread-safe.
	 */
	private def detroyInnerContext {
		var context = owner.getAgentLife(this.factories, this.agentLevelInjector).setInnerContext(null)
		if (context !== null) {
			var ^space = context.defaultSpace
			// Unregister the agent from the default space
			val listener = associatedEventBusListener
			^space.unregister(listener)
			// Destroy the context
			this.contextService.removeContext(context.ID)
		}
	}

	protected override uninstall {
		detroyInnerContext
	}

	def getInnerContext : AgentContext {
		val myself = owner
		var life = myself.getAgentLife(this.factories, this.agentLevelInjector)
		var context = life.getInnerContext [
			val spaceId = it.addressInInnerDefaultSpace.spaceID
			val nctx= this.contextService.createContext(spaceId.contextID, spaceId.ID, myself)
			// Register the agent in the default space
			val ^space = nctx.defaultSpace
			val listener = associatedEventBusListener
			^space.registerStrongParticipant(listener)
			return nctx
		]
		return context
	}

	def getInnerDefaultSpace : EventSpace {
		return getInnerContext.defaultSpace
	}

	def isInInnerDefaultSpace(^event : Event) : boolean {
		if (^event !== null) {
			val adr = ^event.source
			if (adr !== null) {
				return adr.spaceID.isInnerDefaultSpace
			}
		}
		return false
	}

	def isInnerDefaultSpace(^space : Space) : boolean {
		isInnerDefaultSpace(^space.spaceID)
	}

	def isInnerDefaultSpace(spaceID : SpaceID) : boolean {
		val context = owner.getAgentLife(this.factories, this.agentLevelInjector).getInnerContext(null)
		context !== null && spaceID == context.defaultSpace.spaceID
	}

	def isInnerDefaultSpace(spaceID : UUID) : boolean {
		val context = owner.getAgentLife(this.factories, this.agentLevelInjector).getInnerContext(null)
		context !== null && spaceID == context.defaultSpace.spaceID.ID
	}

	def getMemberAgentCount : int {
		val context = owner.getAgentLife(this.factories, this.agentLevelInjector).getInnerContext(null)
		if (context !== null) {
			val nb = context.defaultSpace.numberOfStrongParticipants
			assert nb >= 1, "The agent must be a participant of its inner conntext's default space"
			return nb - 1
		}
		return 0
	}

	def getMemberAgents : ConcurrentSet<UUID> {
		val context = owner.getAgentLife(this.factories, this.agentLevelInjector).getInnerContext(null)
		if (context !== null) {
			var filtered = this.factories.get.concurrentCollectionFactory.newSet(null)
			val oid = this.owner.ID
			context.defaultSpace.forEachStrongParticipant [
				if (it != oid) {
					filtered.add(it)
				}
			]
			return filtered
		}
		return this.factories.get.concurrentCollectionFactory.newSet(null)
	}

	def hasMemberAgent : boolean {
		val context = owner.getAgentLife(this.factories, this.agentLevelInjector).getInnerContext(null)
		if (context !== null) {
			return !context.defaultSpace.isPseudoEmpty(owner.ID)
		}
		return false;
	}

}
