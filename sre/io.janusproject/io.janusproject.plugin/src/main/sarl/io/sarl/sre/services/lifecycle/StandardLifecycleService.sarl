/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2021 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.services.lifecycle

import com.google.inject.Injector
import io.sarl.lang.annotation.Injectable
import io.sarl.lang.core.Agent
import io.sarl.lang.core.DynamicSkillProvider
import io.sarl.sarlspecification.SarlSpecificationChecker
import io.sarl.sre.KernelScope
import io.sarl.sre.boot.configs.SreConfig
import io.sarl.sre.boot.configs.subconfigs.LifecycleConfig
import io.sarl.sre.internal.ConfigurableAgentInjector
import io.sarl.sre.internal.Factories
import io.sarl.sre.services.context.ExternalContextMemberListener
import io.sarl.sre.services.executor.ExecutorService
import io.sarl.sre.services.logging.LoggingService
import java.util.UUID
import javax.inject.Inject
import javax.inject.Provider

/** 
 * Implementation of a spawning service that is using standard object creation
 * for creating the agents.
 * No injection mechanism is used.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.11
 */
class StandardLifecycleService extends AbstractLifecycleService {

	/**
	 * Constructs the service.
	 * 
	 * @param injector the injector to be used.
	 * @param sarlSpecificationChecker the tool for checking the validity of the SARL specification supported by
	 * the agents to launch.
	 * @param skillProvider the provider of skills that should be used to init the agents.
	 * @param lifecycleListener the listener at the platform scale on life cycle events.
	 * @param externalContextListener the listener at the platform scale on context membership events.
	 * @param factories the provider of factories tool.
	 * @param agentLevelInjector the dynamic injector for the agent level.
	 * @param skillUninstaller the object this is able to uninstall the skills.
	 * @param executor the executor service.
	 * @param logger the logging service.
	 * @param sreConfig the configuration of the SRE.
	 */
	@Inject
	new (injector : Injector,
		sarlSpecificationChecker : SarlSpecificationChecker,
		@KernelScope lifecycleListener : Provider<LifecycleServiceListener>,
		@KernelScope externalContextListener : Provider<ExternalContextMemberListener>,
		factories : Provider<Factories>,
		agentLevelInjector : Provider<ConfigurableAgentInjector>,
		skillProvider : DynamicSkillProvider,
		skillUninstaller : SkillUninstaller,
		executor : ExecutorService,
		logger : LoggingService,
		sreConfig : SreConfig) {
		this(sarlSpecificationChecker, sreConfig.createFactory(injector),
			lifecycleListener, externalContextListener, factories, agentLevelInjector,
			skillProvider, skillUninstaller, executor, logger,
			sreConfig.services.lifecycle)
	}

	private static def createFactory(sreConfig : SreConfig, injector : Injector) : AgentCreatorProvider {
		if (sreConfig.services.lifecycle.enableInjectableSupport) {
			return new CreatorFactoryWithInjectableSupport(injector)
		}
		return new CreatorFactoryWithoutInjectableSupport
	}

	/**
	 * Constructs the service.
	 * 
	 * @param sarlSpecificationChecker the tool for checking the validity of the SARL specification supported by
	 * the agents to launch.
	 * @param creator the provider of agent creators that is used when spawning agents.
	 * @param lifecycleListener the listener at the platform scale on life cycle events.
	 * @param externalContextListener the listener at the platform scale on context membership events.
	 * @param factories the provider of factories tool.
	 * @param agentLevelInjector the dynamic injector for the agent level.
	 * @param skillProvider the system-wide provider of agent skills.
	 * @param skillUninstaller the object this is able to uninstall the skills.
	 * @param executor the executor service.
	 * @param logger the logging service.
	 * @param lifecycleConfig the configuration of the lifecycle service.
	 */
	new (sarlSpecificationChecker : SarlSpecificationChecker, creator : AgentCreatorProvider,
		lifecycleListener : Provider<LifecycleServiceListener>,
		externalContextListener : Provider<ExternalContextMemberListener>,
		factories : Provider<Factories>, agentLevelInjector : Provider<ConfigurableAgentInjector>,
		skillProvider : DynamicSkillProvider, skillUninstaller : SkillUninstaller,
		executor : ExecutorService, logger : LoggingService,
		lifecycleConfig : LifecycleConfig) {
		super(sarlSpecificationChecker, creator,
			lifecycleListener, externalContextListener, factories, agentLevelInjector,
			skillProvider, skillUninstaller, executor, logger, lifecycleConfig)
	}

	/** 
	 * A factory of agent creator that provides the parent ID and agent ID to the created agent.
	 * This factory does not support the {@code @Injectable} annotation.
	 * 
	 * @author $Author: sgalland$
	 * @version $FullVersion$
	 * @mavengroupid $GroupId$
	 * @mavenartifactid $ArtifactId$
	 * @since 0.11
	 */
	private static class CreatorFactoryWithoutInjectableSupport extends AbtractAgentCreatorProvider {

		override getAgentCreator(agentType : Class<? extends Agent>, parent : UUID, nbAgents : int,
				skillProviders : DynamicSkillProvider*) : (UUID)=>Agent {
			val cons = agentType.agentConstructor
			return [
				return cons.createAgent(agentType, parent, it, skillProviders)
			]
		}
	
	}

	/** 
	 * A factory of agent creator that provides the parent ID and agent ID to the created agent.
	 * This factory supports the {@code @Injectable} annotation.
	 * 
	 * @author $Author: sgalland$
	 * @version $FullVersion$
	 * @mavengroupid $GroupId$
	 * @mavenartifactid $ArtifactId$
	 * @since 0.11
	 */
	private static class CreatorFactoryWithInjectableSupport extends AbtractAgentCreatorProvider {

		val injector : Injector

		new (injector : Injector) {
			this.injector = injector
		}

		override getAgentCreator(agentType : Class<? extends Agent>, parent : UUID, nbAgents : int,
				skillProviders : DynamicSkillProvider*) : (UUID)=>Agent {
			val cons = agentType.agentConstructor
			return [
				val ag = cons.createAgent(agentType, parent, it, skillProviders)
				assert ag !== null
				if (ag.class.isAnnotationPresent(typeof(Injectable))) {
					this.injector.injectMembers(ag)
				}
				return ag
			]
		}
	
	}

}
