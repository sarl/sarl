/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2021 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.sre.internal.eventguard

import io.sarl.lang.core.Event
import io.sarl.lang.util.ConcurrentCollection
import io.sarl.lang.util.ConcurrentSet
import io.sarl.sre.internal.Factories
import io.sarl.sre.internal.MutableBoolean
import io.sarl.sre.internal.ObjectComparator
import java.lang.reflect.Method
import java.util.Set
import java.util.TreeSet
import java.util.concurrent.ConcurrentMap
import javax.inject.Provider

import static extension io.sarl.sre.internal.eventguard.reflect.StaticReflectBehaviorGuardEvaluatorDictionary.*

/** 
 * Registry of all {@code BehaviorGuardEvaluator} classes containing a method to evaluate the guard of a
 * given behavior (on clause in SARL behavior).
 * 
 * <p>This class is thread-safe.
 *
 * <p>Two types of listeners are supported:<ul>
 * <li><i>Registered listener</i>: it is registered with the function of the same name.
 *     It is notified when an event should be provided to anyone;</li>
 * <li><i>Direct-access listener</i>: it is not registered (if it is, it does not matter).
 *     It is notified when it is passed as argument of a notification function. In this
 *     case, only this listener will receive the event.</li>
 * </ul>
 *
 * @param <REGT> the type of stored data for registered listeners.
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
abstract class AbstractBehaviorGuardEvaluatorRegistry<REGT> implements IBehaviorGuardEvaluatorRegistry {

	protected val factories : Factories

	new (factories : Provider<Factories>) {
		this.factories = factories.get
	}

	/** Replies the internal data structure for the registered listeners.
	 *
	 * @return the internal data structure
	 */
	@Pure
	abstract def getInternalDataStructureForRegisteredListeners : ConcurrentMap<Class<? extends Event>, ConcurrentSet<REGT>>

	/** Replies the mapper from the internal guard evaluator description of register listeners to the public description.
	 * 
	 * @return the internal data structure
	 */
	@Pure
	protected abstract def getInternalEvaluatorMapperForRegisteredListeners : (REGT)=>IBehaviorGuardEvaluator

	/** Replies the mapper from the internal guard evaluator description of direct-access listeners to the public description.
	 * 
	 * @param listener the listener for which the guard evaluator should be replied.
	 * @return the internal data structure
	 * @since 0.12
	 */
	@Pure
	protected def getInternalEvaluatorMapperForDirectAccessListeners(listener : Object) : (Method)=>IBehaviorGuardEvaluator {
		[
			new ReflectBehaviorGuardEvaluator(listener, it)
		]
	}

	/** Replies the filtering of evaluator.
	 * 
	 * @return the filter
	 */
	@Pure
	protected abstract def getInternalEvaluatorFilterForRegisteredListeners : (REGT, Event)=>boolean

	override unregisterAll(callback : (Object)=>boolean) : void {
		val removedListeners = new TreeSet(ObjectComparator::SINGLETON)
		val dt = internalDataStructureForRegisteredListeners
		if (callback !== null) {
			val hasCallback = new MutableBoolean(true)
			val iterator0 = dt.values.iterator
			val mapper = getInternalEvaluatorMapperForRegisteredListeners
			while (hasCallback.get && iterator0.hasNext) {
				val iterator1 = iterator0.next.iterator
				while (hasCallback.get && iterator1.hasNext) {
					val evaluator = iterator1.next
					val target = mapper.apply(evaluator).target
					if (removedListeners.add(target)) {
						val continueCallbacks = callback.apply(target)
						if (!continueCallbacks) {
							hasCallback.set(false)
						}
					}
				}
			}
		}
		// TODO: Is it the most efficient way to clear the map?
		dt.clear
	}

	override unregister(listenerType : Class<?>, callback : (Object)=>boolean) {
		assert listenerType !== null
		val notifiedListeners = new TreeSet(ObjectComparator::SINGLETON)
		val hasCallback = new MutableBoolean(callback !== null)
		val mapper = getInternalEvaluatorMapperForRegisteredListeners
		for (guardedEvaluators : internalDataStructureForRegisteredListeners.values) {
			guardedEvaluators.removeIf [
				val target = mapper.apply(it).target
				assert target !== null
				if (listenerType.isInstance(target)) {
					if (hasCallback.get && notifiedListeners.add(target)) {
						val continueCalls = callback.apply(target)
						if (!continueCalls) {
							hasCallback.set(false)
						}
					}
					return true
				}
				return false
			]

			// TODO: Is the following behavior efficient?
			// don't try to remove the set if it's empty; that can't be done safely without a lock
			// anyway, if the set is empty it'll just be wrapping an array of length 0
		}
	}

	@Pure
	override hasRegisteredEventListener(type : Class<?>) : boolean {
		if (type !== null) {
			val mapper = getInternalEvaluatorMapperForRegisteredListeners
			for (guardedEvaluators : internalDataStructureForRegisteredListeners.values) {
				for (evaluator : guardedEvaluators) {
					if (type.isInstance(mapper.apply(evaluator).target)) {
						return true
					}
				}
			}
		}
		return false
	}

	@Pure
	override getBehaviorGuardEvaluatorsForRegisteredListeners(^event : Event) : ConcurrentCollection<? extends IBehaviorGuardEvaluator> {
		assert ^event !== null
		val eventTypes = ^event.class.flattenHierarchy
		val allEvaluators = this.factories.concurrentCollectionFactory.newCollection
		val mapper = getInternalEvaluatorMapperForRegisteredListeners
		val filter = getInternalEvaluatorFilterForRegisteredListeners
		val dt = internalDataStructureForRegisteredListeners
		for (eventType : eventTypes) {
			val eventSubscribers = dt.get(eventType)
			if (eventSubscribers !== null) {
				for (guardedEvaluator : eventSubscribers) {
					if (filter.apply(guardedEvaluator, ^event)) {
						allEvaluators += mapper.apply(guardedEvaluator)
					}
				}
			}
		}
		return allEvaluators
	}

	override getRegisteredEventListeners(type : Class<TT>, collection : Set<? super TT>) : int with TT {
		assert type !== null
		assert collection !== null
		var nb = 0
		val mapper = getInternalEvaluatorMapperForRegisteredListeners
		for (guardedEvaluators : internalDataStructureForRegisteredListeners.values) {
			for (evaluator : guardedEvaluators) {
				val target = mapper.apply(evaluator).target
				if (type.isInstance(target)) {
					if (collection += type.cast(target)) {
						nb++
					}
				}
			}
		}
		return nb
	}

	@Pure
	override getRegisteredEventListeners(type : Class<TT>) : ConcurrentSet<TT> with TT {
		var result = this.factories.concurrentCollectionFactory.<TT>newSet(ObjectComparator::SINGLETON)
		getRegisteredEventListeners(type, result)
		return result
	}

}
