/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2019 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License")
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.sre.boot.commands

import io.bootique.BQModule
import io.bootique.BQModuleProvider
import io.bootique.cli.Cli
import io.bootique.command.CommandOutcome
import io.bootique.meta.application.CommandMetadata
import io.sarl.sre.boot.configs.SreConfig

import static extension io.bootique.BQCoreModule.extend
import java.text.MessageFormat
import java.util.concurrent.locks.ReadWriteLock
import javax.inject.Provider
import com.google.inject.AbstractModule
import javax.inject.Singleton
import com.google.inject.Provides
import com.google.inject.Module

/**
 * Command for running the SRE with a single agent at boot time.
 * The first command-line argument that is a agent's class is considered
 * as the agent to be launched.
 *
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.8
 */
class RunSingleAgentCommand extends AbstractRunCommand {

	/** Constructor.
	 *
	 * @param configuration the configuration of the tool.
	 * @param lock the lock to use for synchronizing.
	 */
	new (configuration : Provider<SreConfig>, lock : ReadWriteLock) {
		super(configuration,
			CommandMetadata
				.builder(typeof(RunSingleAgentCommand))
				.description(Messages::RunSingleAgentCommand_0),
			lock)
	}

	override run(cli : Cli) : CommandOutcome {
		var args = cli.standaloneArguments()
		if (args.empty) {
			return CommandOutcome::failed(255, Messages::RunSingleAgentCommand_1)
		}

		try {
			val bootstrap = getSREBootstrap
			if (bootstrap === null) {
				return CommandOutcome::failed(255, Messages::RunSingleAgentCommand_3)
			}
			var config = cli.synchronizeCliWithBootConfig(1)
			bootstrap.startAgentsFromConfig(config)
			bootstrap.waitForTermination
		} catch (exception : Throwable) {
			return CommandOutcome.failed(255,
				MessageFormat::format(Messages::RunSingleAgentCommand_2, exception.localizedMessage), exception)
		}
		
		return CommandOutcome::succeeded
	}

}

/** Module for the command for running the SRE with a single agent.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.9
 */
class RunSingleAgentCommandModule extends AbstractModule {

	override configure {
		binder.extend => [
			addCommand(typeof(RunSingleAgentCommand))
			defaultCommand = typeof(RunSingleAgentCommand)
		]
	}

	/** Provide the command for running the SRE.
	 * 
	 * @param configuration the configuration of the tool.
	 * @param lock the provider of lock.
	 * @return the command.
	 */
	@Provides
	@Singleton
	def provideRunCommand(configuration : Provider<SreConfig>, lock : Provider<ReadWriteLock>) : RunSingleAgentCommand {
		return new RunSingleAgentCommand(configuration, lock.get)
	}

}

/** Provider of the module for running the SRE with a single agent.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.9
 */
class RunSingleAgentCommandModuleProvider implements BQModuleProvider {

	override module : Module {
		return new RunSingleAgentCommandModule
	}

	override moduleBuilder : BQModule.Builder {
		return BQModule.builder(module).overrides(overrides).providerName(name).configs(configs).description(
			Messages::RunSingleAgentCommandModuleProvider_0);
	}

}
