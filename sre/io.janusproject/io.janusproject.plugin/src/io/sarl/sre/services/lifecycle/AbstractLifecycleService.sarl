/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2019 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.services.lifecycle

import io.sarl.lang.core.Agent
import io.sarl.lang.core.DynamicSkillProvider
import io.sarl.sarlspecification.SarlSpecificationChecker
import io.sarl.sre.internal.SmartListenerCollection
import io.sarl.sre.services.AbstractSreService
import io.sarl.sre.services.context.Context
import io.sarl.sre.services.context.ExternalContextMemberListener
import io.sarl.sre.services.executor.ExecutorService
import io.sarl.sre.services.logging.LoggingService
import io.sarl.util.concurrent.Collections3
import java.text.MessageFormat
import java.util.ArrayList
import java.util.List
import java.util.UUID
import java.util.concurrent.locks.ReadWriteLock
import javax.inject.Provider
import javax.inject.Singleton

import static io.sarl.sre.services.lifecycle.AgentLife.*
import org.eclipse.xtend.lib.annotations.Accessors

/** 
 * Implementation of a spawning service that is based on the other services of the SRE platform.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
@Singleton
abstract class AbstractLifecycleService extends AbstractSreService implements LifecycleService {

	/** Maximum number of agents to be launch by a single thread.
	 */
	static val CREATION_POOL_SIZE = 128

	val globalListeners : SmartListenerCollection<?>

	val sarlSpecificationChecker : SarlSpecificationChecker

	val agentCreatorProvider : AgentCreatorProvider

	val skillUninstaller : SkillUninstaller

	val logger : LoggingService

	val platformLifecycleEventEmitter : LifecycleServiceListener

	val platformContextEventEmitter : ExternalContextMemberListener

	val executor : ExecutorService

	var userDynamicSkillProviders : List<DynamicSkillProvider>

	@Accessors(PROTECTED_GETTER)
	val lockProvider : Provider<ReadWriteLock>

	val userDynamicSkillProvidersLock : ReadWriteLock

	/** 
	 * Constructs the service.
	 * 
	 * @param sarlSpecificationChecker the tool for checking the validity of the SARL specification supported by
	 *     the agents to launch.
	 * @param creator the provider of agent creators that is used when spawning agents.
	 * @param globalListeners is the collection of global listeners to use.
	 * @param lockProvider the provider of locks.
	 * @param lifecycleListener the listener at the platform scale on life cycle events.
	 * @param externalContextListener the listener at the platform scale on context membership events.
	 * @param skillUninstaller the object this is able to uninstall the skills.
	 * @param executor the executor service.
	 * @param logger the logging service.
	 */
	new (
		sarlSpecificationChecker : SarlSpecificationChecker,
		creator : AgentCreatorProvider,
		globalListeners : SmartListenerCollection<?>, lockProvider : Provider<ReadWriteLock>,
		lifecycleListener : Provider<LifecycleServiceListener>,
		externalContextListener : Provider<ExternalContextMemberListener>,
		skillUninstaller : SkillUninstaller, executor : ExecutorService, logger : LoggingService) {
		this.sarlSpecificationChecker = sarlSpecificationChecker
		this.agentCreatorProvider = creator
		this.globalListeners = globalListeners
		this.lockProvider = lockProvider
		this.userDynamicSkillProvidersLock = lockProvider.get
		this.skillUninstaller = skillUninstaller
		this.executor = executor
		this.logger = logger
		this.platformLifecycleEventEmitter = lifecycleListener.get
		this.platformContextEventEmitter = externalContextListener.get
	}
	
	/** Replies the backend logging service.
	 *
	 * @return the logging service.
	 */
	protected def getLoggingService : LoggingService {
		this.logger
	}

	protected def onStart {
		this.platformLifecycleEventEmitter.addLifecycleServiceListener
	}

	protected def onStop {
		this.platformLifecycleEventEmitter.removeLifecycleServiceListener
	}
	
	def addLifecycleServiceListener(agentLifecycleListener : LifecycleServiceListener) {
		this.globalListeners.add(typeof(LifecycleServiceListener), agentLifecycleListener)
	}

	def removeLifecycleServiceListener(agentLifecycleListener : LifecycleServiceListener) {
		this.globalListeners.remove(typeof(LifecycleServiceListener), agentLifecycleListener)
	}

	def addKernelAgentLifecycleListener(listener : KernelAgentLifecycleListener) {
		this.globalListeners.add(typeof(KernelAgentLifecycleListener), listener)
	}

	def removeKernelAgentLifecycleListener(listener : KernelAgentLifecycleListener) {
		this.globalListeners.remove(typeof(KernelAgentLifecycleListener), listener)
	}

	/** Notify the listeners outside the parent context.
	 * 
	 * @param agents the destroyed agent.
	 * @param outerContexts the contexts in which the agent w destroyed.
	 */
	protected def fireAgentDestroy(^agent : Agent, outerContexts : Iterable<ContextReference>) {
		// Notify the listeners on the spawn events (not restricted to a single agent)
		this.globalListeners.notifyListeners(typeof(LifecycleServiceListener)) [
			agentDestroyed(^agent, outerContexts)
		]
	}
	
	/** Notify the listeners outside the parent context.
	 *
	 * @param spawningAgent the spawning agent.
	 * @param context the context in which the agents were spawned.
	 * @param agentClazz the type of the spawned agents.
	 * @param agents the spawned agents.
	 * @param initializationParameters the initialization parameters.
	 */
	protected def fireAgentSpawned(spawningAgent : UUID, context : Context,
			agentClazz : Class<? extends Agent>, agents : List<Agent>, initializationParameters : Object*) {
		this.globalListeners.notifyListeners(typeof(LifecycleServiceListener)) [
			agentSpawned(spawningAgent, context, agentClazz, agents, initializationParameters)
		]
	}

	/**
	 * Notifies the listeners about the kernel agent destruction.
	 */
	protected def fireKernelAgentDestroy {
		this.globalListeners.notifyListeners(typeof(KernelAgentLifecycleListener)) [
			kernelAgentDestroyed
		]
	}

	def isKillableAgent(^agent : Agent) : boolean {
		try {
			var innerContext = getLife(^agent, this.lockProvider).innerContext
			if (innerContext !== null) {
				var participants = innerContext.defaultSpace.getParticipants
				if (participants !== null) {
					var lck = participants.lock
					lck.readLock.lock
					try {
						if (participants.size > 1 || (participants.size == 1 && !participants.contains(^agent.ID))) {
							return false
						} 
					} finally {
						lck.readLock.unlock
					}
				}
			}
			return true
		} catch (exception : Throwable) {
			return false
		}
	}

	def spawnAgent(nbAgents : int, spawningAgent : UUID, parent : Context, agentId : UUID,
		agentClazz : Class<? extends Agent>, params : Object*) : SpawnResult {
		spawnAgent(nbAgents, spawningAgent, parent, agentClazz, params) [
			if (agentId !== null && it === 0) {
				return agentId
			}
			return UUID::randomUUID
		]
	}
	
	/** Replies if the service can spawn.
	 *
	 * @return {@code true} if the service can spawn an agent.
	 */
	def canSpawnAgent : boolean {
		isRunning
	}
	
	protected def spawnAgent(nbAgents : int, spawningAgent : UUID, parent : Context, agentClazz : Class<? extends Agent>, params : Object[],
		agentIds : (int) => UUID) : SpawnResult {

		val agents = new ArrayList<Agent>(nbAgents)
		val agentsLock = this.lockProvider.get
		val errors = new ArrayList<Throwable>()
		if (canSpawnAgent && nbAgents > 0) {
			// Create the list of the spawned agents during this function execution
			try {
				// Check if the version of the SARL agent class is compatible.
				if (this.sarlSpecificationChecker === null || !this.sarlSpecificationChecker.isValidSarlElement(agentClazz)) {
					errors += new InvalidSarlSpecificationException(agentClazz)
				} else {
					// Create the shared injector that is also able to create the agent instance.
					val agentInstanceCreator = this.agentCreatorProvider.getAgentCreator(agentClazz, parent.ID,
						nbAgents, dynamicSkillProviders, agentIds)
					// Create the block of code for creating a single agent
					val agentCreator : Runnable = [
						val ^agent = agentInstanceCreator.get()
						if (^agent === null) {
							throw new CannotSpawnException(agentClazz)
						}
						// Start the agent's life
						var life = getLife(^agent, this.lockProvider)
						try {
							var started = false
							try {
								started = life.start(this.skillUninstaller, getLoggingService, spawningAgent, parent,
									params)
							} catch (e : Throwable) {
								errors += new CannotSpawnException(agentClazz, e)
							}
							if (started) {
								// Add the agent in the system. It is synchronized because additions may occur in parallel
								agentsLock.writeLock.lock
								try {
									agents += ^agent
								} finally {
									agentsLock.writeLock.unlock
								}
								^agent.onAgentCreated
							} else if (parent.emptyRootContext) {
								// The agent seems to be the boot agent, and it has failed to be launched.
								// Moreover the root context is still empty.
								// The safer decision should be to stop the kernel.
								onAgentFrameworkStop
							}
						} catch (e : Throwable) {
							errors += new CannotSpawnException(agentClazz, e)
						}
					]
					// Create a single agent with a sequential call; or multiple agents in parallel
					if (nbAgents > 1) {
						this.executor.executeBlockingTask(nbAgents, CREATION_POOL_SIZE, agentCreator)
					} else {
						agentCreator.run
					}
					// Fire the general spawning event
					if (!agents.empty) {
						fireAgentSpawningEvents(spawningAgent, parent, agentClazz, params, agents)
					}
				}
			} catch (e : CannotSpawnException) {
				errors += e
			} catch (e : Throwable) {
				errors += new CannotSpawnException(agentClazz, e)
			}
		} else {
			errors += new SpawnDisabledException(parent.ID, agentClazz)
		}
		return new SpawnResult(
			(agents.map[it.ID]).unmodifiableView,
			errors)
	}

	/** This function is called each time the agent framework should be stopped.
	 * This function is provided for being overridden by the sub-classes of
	 * the current class.
	 * 
	 * <p>Within the {@code AbstractLifecycleService}, this function fires the kernel agent
	 * destruction event.
	 * 
	 * @since 0.8.0
	 */
	protected def onAgentFrameworkStop {
		fireKernelAgentDestroy
	}

	/** This function is called each time an agent was created.
	 * This function is provided for being overridden by the sub-classes of
	 * the current class.
	 *
	 * <p>Within the {@code AbstractLifecycleService}, this function does nothing.
	 *
	 * @param agent the spawned agent.
	 * @since 0.8.0
	 */
	protected def onAgentCreated(^agent : Agent) {
		//
	}

	/** Fire the events related to the agent spawn.
	 *
	 * @param spawningAgent the creator.
	 * @param parent the context in which the agents are created.
	 * @param agentClazz the type of the spawned agents.
	 * @param spawnedAgents the spawnedAgents.
	 */
	protected def fireAgentSpawningEvents(spawningAgent : UUID, parent : Context,
		agentClazz : Class<? extends Agent>, params : Object[], spawnedAgents : List<Agent>) {
		spawningAgent.fireAgentSpawned(parent, agentClazz, spawnedAgents, params)
		val spaceID = parent.defaultSpace.spaceID
		for (^agent : spawnedAgents) {
			this.platformContextEventEmitter.memberJoined(
				parent, spaceID,
				^agent.ID, agentClazz.name)
		}
	}

	/** Replies if the given context is the root context and is empty.
	 *
	 * @param context the agent context to test.
	 * @return {@code true} if the given context is the root context, and it is empty.
	 *     @Otherwise {@code false}.
	 */
	protected def isEmptyRootContext(context : ContextReference) {
		context.context.isEmptyRootContext
	}

	/** Replies if the given context is the root context and is empty.
	 * 
	 * @param context the agent context to test.
	 * @return {@code true} if the given context is the root context, and it is empty.
	 * @Otherwise {@code false}.
	 */
	protected def isEmptyRootContext(context : Context) {
		context !== null && context.rootContext && context.defaultSpace.participants.empty
	}

	
	def killAgent(^agent : Agent) : boolean {
		// We should check if it is possible to kill the agent BEFORE killing it.
		if (this.running && ^agent.isKillableAgent()) {
			var life = getLife(^agent, this.lockProvider)
			var defaultContext = life.defaultContext
			var contexts = life.stop(this.skillUninstaller, getLoggingService)
			^agent.onAgentKilled
			fireAgentDestructionEvents(^agent, defaultContext, contexts)
			return true
		}

		return false
	}

	/** This function is called each time an agent was killed.
	 * This function is provided for being overridden by the sub-classes of
	 * the current class.
	 * 
	 * <p>Within the {@code AbstractLifecycleService}, this function does nothing.
	 * 
	 * @param agent the killed agent.
	 * @since 0.8.0
	 */
	protected def onAgentKilled(^agent : Agent) {
		//
	}

	protected def fireAgentDestructionEvents(^agent : Agent, defaultContextBeforeKilled : ContextReference,
		leavedContexts : Iterable<ContextReference>) {
		// Leave the default context because the ExternalContextAccessSkill does not fire events related
		// to the default space.
		this.platformContextEventEmitter.memberLeft(
			defaultContextBeforeKilled.context,
			defaultContextBeforeKilled.addressInDefaultSpace.spaceID,
			^agent.ID, ^agent.class.name)
		// Fire the agent destroy event
		^agent.fireAgentDestroy(leavedContexts)
		// Test if the agent is the latest on this kernel.
		if (defaultContextBeforeKilled.emptyRootContext) {
			onAgentFrameworkStop
		}
	}

	/** Replies the dynamic skill providers that are defined by the user of the service. */
	protected def getDynamicSkillProviders : Iterable<DynamicSkillProvider> {
		this.userDynamicSkillProvidersLock.readLock.lock
		try {
			if (this.userDynamicSkillProviders === null) {
				return emptyList
			}
			return Collections3::unmodifiableSynchronizedIterable(this.userDynamicSkillProviders, this.userDynamicSkillProvidersLock)
		} finally {
			this.userDynamicSkillProvidersLock.readLock.unlock
		}
	}

	/** Add a dynamic skill provider that must be used for the agents that are spawned
	 * after the call to this function.
	 * 
	 * @param provider is the provider of skills for newly created agents.
	 */
	def addDynamicSkillProvider(provider : DynamicSkillProvider) {
		assert provider !== null
		this.userDynamicSkillProvidersLock.writeLock.lock
		try {
			if (this.userDynamicSkillProviders === null) {
				this.userDynamicSkillProviders = newArrayList
			}
			this.userDynamicSkillProviders += provider
		} finally {
			this.userDynamicSkillProvidersLock.writeLock.unlock
		}
	}

	/** Remove a dynamic skill provider that must be not be used any more for the agents that are spawned.
	 * 
	 * @param provider is the provider of skills to be removed.
	 */
	def removeDynamicSkillProvider(provider : DynamicSkillProvider) {
		assert provider !== null
		var list : List<DynamicSkillProvider>
		this.userDynamicSkillProvidersLock.readLock.lock
		try {
			list = this.userDynamicSkillProviders
		} finally {
			this.userDynamicSkillProvidersLock.readLock.unlock
		}
		if (list !== null) {
			this.userDynamicSkillProvidersLock.writeLock.lock
			try {
				list = this.userDynamicSkillProviders
				if (list !== null) {
					this.userDynamicSkillProviders.remove(provider)
					if (this.userDynamicSkillProviders.empty) {
						this.userDynamicSkillProviders = null
					}
				}
			} finally {
				this.userDynamicSkillProvidersLock.writeLock.unlock
			}
		}
	}

}

/**
 * This exception is thrown when the agent to spawn is not generated according to a valid SARL specification version.
 *
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
class InvalidSarlSpecificationException extends RuntimeException {

	new (agentType : Class<? extends Agent> ) {
		super(MessageFormat::format(Messages::InvalidSarlSpecificationException_0, agentType.name))
	}

}

/** 
 * This exception is thrown when the spawning service of agents is disabled.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
class SpawnDisabledException extends RuntimeException {

	/**
	 * @param parentID
	 *            - the identifier of the parent entity that is creating the agent.
	 * @param agentClazz
	 *            - the type of the agent to spawn.
	 */
	new (parentID : UUID, agentClazz : Class<? extends Agent>) {
		super(MessageFormat.format(Messages.SpawnDisabledException_0, parentID, agentClazz));
	}

}
