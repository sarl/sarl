/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2019 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.skills.bic

import io.sarl.core.DefaultContextInteractions
import io.sarl.lang.core.Address
import io.sarl.lang.core.Agent
import io.sarl.lang.core.AgentContext
import io.sarl.lang.core.Event
import io.sarl.lang.core.EventSpace
import io.sarl.lang.core.Scope
import io.sarl.lang.core.Space
import io.sarl.lang.core.SpaceID
import io.sarl.sre.skills.BuiltinSkill
import java.util.UUID
import java.util.concurrent.locks.ReadWriteLock
import javax.inject.Inject
import javax.inject.Provider

import static io.sarl.sre.services.lifecycle.AgentLife.*

/** 
 * Skill to access to the default interaction context.
 * 
 * @author $Author: srodriguez$
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
skill DefaultContextInteractionsSkill extends BuiltinSkill implements DefaultContextInteractions {

	/** Constructor. */
	@Inject
	new (lockProvider : Provider<ReadWriteLock>) {
		super(lockProvider)
	}

	def getDefaultContext : AgentContext {
		getLife(owner, this.lockProvider).defaultContext.context
	}

	def getDefaultSpace : EventSpace {
		getLife(owner, this.lockProvider).defaultContext.defaultSpace
	}

	def getDefaultAddress : Address {
		getLife(owner, this.lockProvider).defaultContext.addressInDefaultSpace
	}

	def isDefaultContext(context : AgentContext) : boolean {
		context.ID.isDefaultContext
	}

	def isDefaultContext(contextID : UUID) : boolean {
		contextID == this.defaultContext.ID
	}

	def isDefaultSpace(^space : Space) : boolean {
		^space.spaceID.isDefaultSpace
	}

	def isDefaultSpace(^space : SpaceID) : boolean {
		^space == this.defaultSpace.spaceID
	}

	def isDefaultSpace(^space : UUID) : boolean {
		^space == this.defaultSpace.spaceID.ID
	}

	def isInDefaultSpace(^event : Event) : boolean {
		if (^event !== null) {
			val adr = ^event.source
			if (adr !== null) {
				return adr.spaceID.isDefaultSpace
			}
		}
		return false
	}

	def emit(^event : Event, scope : Scope<Address> = null) {
		^event.source = this.defaultAddress
		this.defaultSpace.emit(ID, ^event, scope)
	}

	def willReceive(receiver : UUID, ^event : Event) {
		^event.emit [it.UUID == receiver]
	}
	
	@Deprecated	
	def receive(receiver : UUID, ^event : Event) {
		receiver.willReceive(^event)
	}

	@Deprecated
	def spawn(agentType : Class<? extends Agent>, params : Object*) : UUID {
		throw new UnsupportedOperationException("Use Lifecycle capacity")
	}

}
